OPEN INFOCAD
OPEN TBINFCAD

OPEN INFOTCE
OPEN TBINFTCE

OPEN RECEITAS

//--
//-- !1 = tipo de informacao
//-- !2 = codigo do tipo informacao
//-- !3 = variavel para receber a descricao
//--
#COMMAND BUSCA_TBINFCAD
 INDICATE @OK! TRUE

 CLEAR INFOCAD
 MOVE PARGERAL.ESTADO    TO INFOCAD.UF
 MOVE PARGERAL.EXERCICIO TO INFOCAD.ANO
 MOVE !1                 TO INFOCAD.DESCRICAO
 FIND EQ INFOCAD BY INDEX.2
 [~FOUND] INDICATE @OK! FALSE
 [ FOUND] BEGIN
           CLEAR TBINFCAD
           MOVE INFOCAD.UF     TO TBINFCAD.UF
           MOVE INFOCAD.ANO    TO TBINFCAD.ANO
           MOVE INFOCAD.CODIGO TO TBINFCAD.COD_INFOCAD
           MOVE !2             TO TBINFCAD.CODIGO
           FIND EQ TBINFCAD BY INDEX.1
           [~FOUND] INDICATE @OK! FALSE
           [ FOUND] MOVE TBINFCAD.DESCRICAO TO !3
          END
#ENDCOMMAND

//--
//-- !1 = tipo de informacao
//-- !2 = codigo do tipo informacao
//-- !3 = variavel para receber a descricao
//--
#COMMAND BUSCA_TBINFTCE
 INDICATE @OK! TRUE

 CLEAR INFOTCE
 MOVE PARGERAL.EXERCICIO TO INFOTCE.ANO
 MOVE PARGERAL.ESTADO    TO INFOTCE.ESTADO
 MOVE !1                 TO INFOTCE.DESCRICAO
 FIND EQ INFOTCE BY INDEX.2
 [~FOUND] INDICATE @OK! FALSE
 [ FOUND] BEGIN
           CLEAR TBINFTCE
           MOVE INFOTCE.ANO    TO TBINFTCE.ANO
           MOVE INFOTCE.ESTADO TO TBINFTCE.ESTADO
           MOVE INFOTCE.CODIGO TO TBINFTCE.COD_INFOTCE
           MOVE !2             TO TBINFTCE.CODIGO
           FIND EQ TBINFTCE BY INDEX.1
           [~FOUND] INDICATE @OK! FALSE
           [ FOUND] MOVE TBINFTCE.DESCRICAO TO !3
          END
#ENDCOMMAND

//--
//-- busca codigo da receita e retorna status da divida ativa
//-- !1 codigo da receita
//-- !2 ano (de 2017 para 2018 houve a mudan‡a de CNR - exercicio da LOA nao sao necessariamente do mesmo ano do pargeral)
//-- !3 a partir de 2022 as receitas .9. foram excluidas - as .3. podem conter DA ou AT
//--
#COMMAND BUSCA_DIVIDA_ATIVA
#IFDEF ST@CODIGO@RECEITA
#ELSE
   STRING ST@CODIGO@RECEITA
#ENDIF
#IFDEF ST@TIPO@DIVIDA@ATIVA
#ELSE
   STRING ST@TIPO@DIVIDA@ATIVA
#ENDIF
#IFDEF ST@NATUREZA@DIVIDA@ATIVA
#ELSE
   STRING ST@NATUREZA@DIVIDA@ATIVA
#ENDIF
 
 MOVEALL '' TO ST@TIPO@DIVIDA@ATIVA ST@NATUREZA@DIVIDA@ATIVA
 
 MOVE (OBTER_CODIGO_PADRAO_RECEITA(!1,!2)) TO ST@CODIGO@RECEITA
 
 INDICATE @DIVIDA_ATIVA!                FALSE
 INDICATE @DIVIDA_ATIVA_TRIBUTARIA!     FALSE
 INDICATE @DIVIDA_ATIVA_NAO_TRIBUTARIA! FALSE
 
 INDICATE @DA_PRINCIPAL!                FALSE
 INDICATE @DA_MULTAS_JUROS!             FALSE
 INDICATE @DA_ATUALIZACAO_MONETARIA!    FALSE
 
 CLEAR RECEITAS
 MOVE PARGERAL.ESTADO   TO RECEITAS.UF
 MOVE !2                TO RECEITAS.ANO
 MOVE ST@CODIGO@RECEITA TO RECEITAS.CODIGO
 FIND EQ RECEITAS BY INDEX.1
 [~FOUND] CLEAR RECEITAS
 
 INDICATE @DIVIDA_ATIVA!                AS RECEITAS.DIVIDA_ATIVA IN 'TN'
 INDICATE @DIVIDA_ATIVA_TRIBUTARIA!     AS RECEITAS.DIVIDA_ATIVA EQ 'T'
 INDICATE @DIVIDA_ATIVA_NAO_TRIBUTARIA! AS RECEITAS.DIVIDA_ATIVA EQ 'N'
 
 INDICATE @DA_PRINCIPAL!                AS RECEITAS.TIPO_DIV_ATIVA EQ 'PR'
 INDICATE @DA_MULTAS_JUROS!             AS RECEITAS.TIPO_DIV_ATIVA EQ 'MJ'
 INDICATE @DA_ATUALIZACAO_MONETARIA!    AS RECEITAS.TIPO_DIV_ATIVA EQ 'AT'
 
 [     @DIVIDA_ATIVA_TRIBUTARIA!] MOVE '1' TO ST@TIPO@DIVIDA@ATIVA
 [ @DIVIDA_ATIVA_NAO_TRIBUTARIA!] MOVE '2' TO ST@TIPO@DIVIDA@ATIVA
 
      IF RECEITAS.TIPO_DIV_ATIVA EQ 'PR' MOVE '01' TO ST@NATUREZA@DIVIDA@ATIVA
 ELSE IF RECEITAS.TIPO_DIV_ATIVA EQ 'AT' MOVE '02' TO ST@NATUREZA@DIVIDA@ATIVA
 ELSE IF RECEITAS.TIPO_DIV_ATIVA EQ 'MJ' MOVE '03' TO ST@NATUREZA@DIVIDA@ATIVA
 
 IF !3 NE '' BEGIN
                   IF !3 EQ 'PR' MOVE '01' TO ST@NATUREZA@DIVIDA@ATIVA
              ELSE IF !3 EQ 'AT' MOVE '02' TO ST@NATUREZA@DIVIDA@ATIVA
             END
 
 INDICATE FOUND TRUE
#ENDCOMMAND
