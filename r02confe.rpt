/TELA RESIDENT
ÆÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍµ
³                                                                              ³
³                                                                              ³
ÃÄÄÄÄÄÄÄ RELATORIO DE CONFERENCIA (XML) DESPESA ORCAMENTARIA EMPENHADA ÄÄÄÄÄÄÄÄ´
³                                                                              ³
³                                                                              ³
³Entidade       <F2>: __ _____________________________________________________ ³
³                                                                              ³
³Mes Inicial                     : __                                          ³
³                                                                              ³
³Mes Final                       : __ <01...13>                                ³
³                                                                              ³
³Acumula dados da Camara         : _   <S/N>                                   ³
³                                                                              ³
³Sintetico ou Analitico          : _   <S/A>                                   ³
³                                                                              ³
³                                                                              ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                               <ESC>  Retorna                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/CONFIRMA RESIDENT
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³______________________________________________________________________________³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/MSG RESIDENT
ÚÄÄÄÄÄÄ RELATORIO DE CONFERENCIA (XML) DESPESA ORCAMENTARIA EMPENHADA ÄÄÄÄÄÄÄ¿
³                                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ SOLICITADO ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                                                                            ³
³                                                                            ³
³           ____________________________________________________             ³
³                                                                            ³
³                                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ PROCESSANDO ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                                                                            ³
³                                                                            ³
³       Dados      : ___________________________________________________     ³
³                                                                            ³
³                                                                            ³
³       Lendo      :   ________.   Imprimindo : _____.    P gs.: ___.        ³
³                                                                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/HEADER RESIDENT
-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
|                                                                               ____________________________________________________________                                                                              |
|                                                                                                                                                                                                                         |
|                                                                               ____________________________________________________________                                                                              |
|                                                                                                                                                                                                                         |
|                                                                                                                                                                                                                         |
|                                                                                         Despesa Orcamentaria - Balancete _________                                                                                      |
|                                                                                                                                                                                                                         |
| DATA  __/__/____                                                              ____________________________________________________________                                                                Pagina _____. |
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|            |                                                     |                      Dotacao                    |             Empenhado           |            Liquidado            |             Saldos              |
|            |                                                     |-------------------------------------------------|---------------------------------|---------------------------------|---------------------------------|
|   DESPESA  |                   Especificacao                     |         Inicial|     Creditos  |      Atualizada|      no Periodo|           Atual|      no Periodo|           Atual|      a Empenhar|      a Liquidar|
|------------------------------------------------------------------|-------------------------------------------------|---------------------------------|---------------------------------|---------------------------------|
/BODY RESIDENT
|____________|_____________________________________________________|_________,___.__|________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|
/LINHA RESIDENT
|            |                                                     |                |               |                |                |                |                |                |                |                |
/LINHA2 RESIDENT
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
/SUBTOTAL RESIDENT
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|            |_____________________________________________________|_________,___.__|________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|
|            |                                                     |                |               |                |                |                |                |                |                |                |
/TOTAL RESIDENT
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|            |_____________________________________________________|_________,___.__|________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|_________,___.__|
|            |                                                     |                |               |                |                |                |                |                |                |                |
 --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
/FOOTER_COMPLETE RESIDENT
|            |                                                     |                |               |                |                |                |                |                |                |                |
/FOOTER RESIDENT
 --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
/*
#INCLUDE CONAM.LIB
#INCLUDE ZERAARQ.LIB
//--
//-- !1 = string st_dados (preenchida pelo monta_dados)
//-- !2 = conta contabil
//-- !3 = mes
//-- !4 = valor_1 (debito)
//-- !5 = valor_2 (credito)
//-- !6 = tipo (Analitica ou Sintetica)
//--
#COMMAND GRAVA_TEMPCALC
 INDICATE GRAVA! AS (LEFT(!2,9)) IN '6.2.2.1.3-5.2.2.1.1-5.2.2.1.2-5.2.2.1.9'
 [ GRAVA!] BEGIN
            CLEAR TEMPCALC
            MOVE ST@CODIGO@ENTIDADE      TO TEMPCALC.COD_ENTIDADE
            MOVE ST@EXERCICIO@FINANCEIRO TO TEMPCALC.EXERCICIO
            MOVE ST@USUARIO              TO TEMPCALC.USUARIO
            MOVE !1                      TO TEMPCALC.CODIGO
            FIND EQ TEMPCALC BY INDEX.1
            INDICATE ERR FALSE
            ON ERROR GOSUB CHECA_GRAVACAO
            [ FOUND] REREAD TEMPCALC
            //--empenhado - liquidado
            IF (LEFT(!2,9)) EQ '6.2.2.1.3' BEGIN
             IF !3 GE JN_MES_INICIAL CALC (TEMPCALC.VALOR_4 + (!5 - !4)) TO TEMPCALC.VALOR_4
                                     CALC (TEMPCALC.VALOR_5 + (!5 - !4)) TO TEMPCALC.VALOR_5
             END
            //--liquidado
            IF !2 IN '6.2.2.1.3.03.00.00-6.2.2.1.3.04.00.00' BEGIN
             IF !3 GE JN_MES_INICIAL CALC (TEMPCALC.VALOR_6 + (!5 - !4)) TO TEMPCALC.VALOR_6
                                     CALC (TEMPCALC.VALOR_7 + (!5 - !4)) TO TEMPCALC.VALOR_7
             END
            //--fixado inicial
            IF (LEFT(!2,9)) EQ '5.2.2.1.1' BEGIN
             [~@SICOM!] BEGIN
               CALC (TEMPCALC.VALOR_1 + (!4 - !5)) TO TEMPCALC.VALOR_1
               CALC (TEMPCALC.VALOR_3 + (!4 - !5)) TO TEMPCALC.VALOR_3
             END  
             [ @SICOM!] IF (LEFT(!2,12)) EQ '5.2.2.1.1.01' BEGIN
               CALC (TEMPCALC.VALOR_1 + (!4 - !5)) TO TEMPCALC.VALOR_1
               CALC (TEMPCALC.VALOR_3 + (!4 - !5)) TO TEMPCALC.VALOR_3
             END  
            END
            //--previsao atualizada
            IF (LEFT(!2,9)) EQ '5.2.2.1.2' BEGIN
             IF !3 GE JN_MES_INICIAL CALC (TEMPCALC.VALOR_2 + (!4 - !5)) TO TEMPCALC.VALOR_2
                                     CALC (TEMPCALC.VALOR_3 + (!4 - !5)) TO TEMPCALC.VALOR_3
             END
            IF PARGERAL.EXERCICIO GE '2019' BEGIN
             IF (LEFT(!2,9)) EQ '5.2.2.1.9' BEGIN
              IF !3 GE JN_MES_INICIAL CALC (TEMPCALC.VALOR_2 + (!4 - !5)) TO TEMPCALC.VALOR_2
                                      CALC (TEMPCALC.VALOR_3 + (!4 - !5)) TO TEMPCALC.VALOR_3
              END
            END 
            ELSE BEGIN
             IF !2 EQ '5.2.2.1.9.04.00.00' BEGIN
              IF !3 GE JN_MES_INICIAL CALC (TEMPCALC.VALOR_2 + (!4 - !5)) TO TEMPCALC.VALOR_2
                                      CALC (TEMPCALC.VALOR_3 + (!4 - !5)) TO TEMPCALC.VALOR_3
              END
            END
             
            MOVE !6 TO TEMPCALC.AUXILIAR
            SAVERECORD TEMPCALC
            UNLOCK
            ON ERROR OFF
           END
#ENDCOMMAND

STRING ST_EXERCICIO 4 ST_MES_INICIAL ST_MES_FINAL ST_PERIODO ST_JUNTA ST_DADOS 255 ST_COD_DESPESA 16 ST_CODIGO_SINTETICO 16 ST_CONTA ST_DESCRICAO ST_QUEBRA
STRING ST_MES_BUSCA 2 ST_PARAMETRO   

INTEGER IT_AUX IT_GRAU RECCOUNT IT_MES_FINAL IT_MES 

AUTOPAGE BODY
NAME JBD_CODIGO               JBD_DESCRICAO   
NAME JBD_FIXADO_INICIAL       JBD_CREDITOS        JBD_FIXADO_FINAL
NAME JBD_EMPENHADO_NO_PERIODO JBD_EMPENHADO_ATUAL JBD_LIQUIDADO_NO_PERIODO JBD_LIQUIDADO_ATUAL
NAME JBD_SALDO_EMPENHAR       JBD_SALDO_LIQUIDAR

AUTOPAGE TOTAL 
NAME JT_TITULO 

AUTOPAGE MSG
NAME JN_PERIODO
NAME JN_DADOS 
NAME JN_LENDO JN_IMPRIMINDO JN_PAGINA

AUTOPAGE TELA
NAME JN_COD_ENTIDADE  JN_ENTIDADE
NAME JN_MES_INICIAL   JN_MES_FINAL
NAME JN_SEPARA_CAMARA
NAME JN_SINTETICO

OPEN ENTIDADE
OPEN TEMPCALC
OPEN ECONORCA
OPEN FONTES
OPEN FUNDOS
ABRE_CONTACOR ST@EXERCICIO@FINANCEIRO CONTACOR

PAGE SET MSG      AT 05 01 COLORS IT_COR_TITULO IT_COR_TITULO
PAGE SET TELA     AT  4  0 COLORS IT_COR_TARJA  IT_COR_MOLDURA
PAGE SET CONFIRMA AT 21 00 COLORS IT_COR_TARJA  IT_COR_MOLDURA

MOVE PARGERAL.EXERCICIO TO ST_EXERCICIO

DEFINE_FONTE_RECURSO PARGERAL.EXERCICIO

INICIO:
SCREENMODE IT_COR_TARJA ON
PAGE TELA

MOVE 'N' TO JN_SEPARA_CAMARA
MOVE 'A' TO JN_SINTETICO

IF PARGERAL.COD_ENTIDADE EQ '01' BEGIN
 ACCEPT JN_COD_ENTIDADE {AUTOCLEAR}
 [KEY.ESCAPE] ABORT
 IF JN_COD_ENTIDADE NE '' BEGIN
                           COMZEROS JN_COD_ENTIDADE 2
                           CLEAR ENTIDADE
                           MOVE ST@EXERCICIO@FINANCEIRO TO ENTIDADE.EXERCICIO 
                           MOVE JN_COD_ENTIDADE         TO ENTIDADE.CODIGO
                           FIND EQ ENTIDADE BY INDEX.1
                           [~FOUND] BEGIN
                                     ADVERTE 'ENTIDADE NŽO CADASTRADA'
                                     CLEARFORM JN_COD_ENTIDADE THRU JN_ENTIDADE
                                     GOTO INICIO
                                    END
                           MOVE ENTIDADE.DESCRICAO TO JN_ENTIDADE
                          END
 ELSE MOVE 'TODAS' TO JN_ENTIDADE
 END
ELSE BEGIN
 MOVE PARGERAL.COD_ENTIDADE   TO JN_COD_ENTIDADE
 CLEAR ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO ENTIDADE.EXERCICIO 
 MOVE JN_COD_ENTIDADE         TO ENTIDADE.CODIGO
 FIND EQ ENTIDADE BY INDEX.1
 [~FOUND] BEGIN
           ADVERTE 'ENTIDADE NŽO CADASTRADA'
           CLEARFORM JN_COD_ENTIDADE THRU JN_ENTIDADE
          END
 MOVE ENTIDADE.DESCRICAO TO JN_ENTIDADE
 END

INDICATE TODAS! AS JN_COD_ENTIDADE EQ ''

REPEAT
 ACCEPT JN_MES_INICIAL      {CAPSLOCK}
 [KEY.ESCAPE] ABORT
 COMZEROS JN_MES_INICIAL 2
 IF ((JN_MES_INICIAL LT '01') OR (JN_MES_INICIAL GT '13')) BEGIN
                                                            MENSAGEM 353
                                                            CLEARFORM JN_MES_INICIAL
                                                           END
UNTIL JN_MES_INICIAL NE ''

REPEAT
 ACCEPT JN_MES_FINAL      {CAPSLOCK}
 [KEY.ESCAPE] ABORT
 COMZEROS JN_MES_FINAL 2
 IF ((JN_MES_FINAL LT '01') OR (JN_MES_FINAL GT '13')) BEGIN
                                                        MENSAGEM 353
                                                        CLEARFORM JN_MES_FINAL
                                                       END
UNTIL JN_MES_FINAL NE ''

REPEAT
 [ TODAS!] ACCEPT JN_SEPARA_CAMARA {AUTOCLEAR,CAPSLOCK}
 [~TODAS!] BEGIN
  IF JN_COD_ENTIDADE NE '02' MOVE 'N' TO JN_SEPARA_CAMARA
  ELSE                       MOVE 'S' TO JN_SEPARA_CAMARA
  END
 [KEY.ESCAPE] ABORT
 IF NOT JN_SEPARA_CAMARA IN 'SsNn' MOVE '' TO JN_SEPARA_CAMARA
UNTIL JN_SEPARA_CAMARA NE '' 
INDICATE SEPARA_CAMARA! AS JN_SEPARA_CAMARA IN 'Nn'

REPEAT
 ACCEPT JN_SINTETICO {AUTOCLEAR,CAPSLOCK}
 [KEY.ESCAPE] ABORT
 IF NOT JN_SINTETICO IN 'SsAa' MOVE '' TO JN_SINTETICO
UNTIL JN_SINTETICO NE '' 
INDICATE SINTETICO! AS JN_SINTETICO IN 'Ss'

MONTH JN_MES_INICIAL ST_MES_INICIAL
APPEND ST_MES_INICIAL ST_EXERCICIO

MONTH JN_MES_FINAL ST_MES_FINAL
APPEND ST_MES_FINAL ST_EXERCICIO

MOVE 'Periodo: ' TO ST_PERIODO
APPEND ST_PERIODO ST_MES_INICIAL ' A ' ST_MES_FINAL

MOVE 'CONFERENCIA(XML) DES. ORCAMETARIA MES ' TO ST_JUNTA
APPEND ST_JUNTA  JN_MES_INICIAL ' A ' JN_MES_FINAL
ABREREL ST_JUNTA '132C+'

CABEC ST_PERIODO JN_PERIODO

PAGE TELA
PAGE MSG
GOTOXY 25 0

APAGA_TEMPCALC

//--monta temporario
//--movimento de despesa pegar at‚ mes 13, no mes 14 ‚ efetuado o encerramento
MOVE JN_MES_FINAL TO IT_MES_FINAL
FOR IT_MES FROM 1 TO IT_MES_FINAL
 MOVE IT_MES TO ST_MES_BUSCA
 COMZEROS ST_MES_BUSCA 2

 CLEAR CONTACOR
 MOVE ST_EXERCICIO TO CONTACOR.ANO
 MOVE ST_MES_BUSCA TO CONTACOR.MES
 [~@SICOM!] MOVE '07'            TO CONTACOR.XML
 [ @SICOM!] MOVE '11'            TO CONTACOR.XML
 [~TODAS!]  MOVE JN_COD_ENTIDADE TO CONTACOR.COD_ENTIDADE
 REPEAT
  FIND GT CONTACOR BY INDEX.4
  [ FOUND] INDICATE FOUND AS CONTACOR.ANO EQ ST_EXERCICIO
  [ FOUND] INDICATE FOUND AS CONTACOR.MES EQ ST_MES_BUSCA
  [ FOUND] BEGIN
            CALC (JN_LENDO + 1) TO JN_LENDO

            [~@SICOM!]               INDICATE OK! AS (TRIM(CONTACOR.XML))  IN '07-08-27'
            [ @SICOM!]               INDICATE OK! AS (TRIM(CONTACOR.XML))  EQ '11'
            [ OK!]         [~TODAS!] INDICATE OK! AS CONTACOR.COD_ENTIDADE EQ JN_COD_ENTIDADE
            [ OK!] [ SEPARA_CAMARA!] INDICATE OK! AS CONTACOR.COD_ENTIDADE NE '02' 
            [ OK!] BEGIN
                    MOVE '' TO ST_COD_DESPESA
                    [~@SICOM!] MOVE (MID(CONTACOR.DADOS,12,55)) TO ST_COD_DESPESA
                    [ @SICOM!] MOVE (MID(CONTACOR.DADOS,12,54)) TO ST_COD_DESPESA
                    MOVE ST_COD_DESPESA                         TO JN_DADOS
                    
                    CLEAR ECONORCA
                    MOVE PARGERAL.ESTADO    TO ECONORCA.UF
                    MOVE PARGERAL.EXERCICIO TO ECONORCA.ANO
                    MOVE ST_COD_DESPESA     TO ECONORCA.CODIGO
                    FIND EQ ECONORCA BY INDEX.1

                    MOVE ECONORCA.GRAU TO IT_GRAU
       
                    [~SINTETICO!] BEGIN
                      //--grava linha analitica
                      MOVE 'A' TO ST_DADOS
                      APPEND ST_DADOS ' ' ST_COD_DESPESA
                      GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'A' 
                    END
                     
                    //--grava linha total geral
                    MOVE 'X TOTAL GERAL' TO ST_DADOS
                    GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'S'

                    //--grava linha valores nas sinteticas
                    FOR IT_AUX FROM 1 TO (IT_GRAU - 1)
                     [~SINTETICO!] BEGIN
                       IF IT_AUX EQ 4 BEGIN
                        MOVE (LEFT(ST_COD_DESPESA,10)) TO ST_CODIGO_SINTETICO
                        APPEND ST_CODIGO_SINTETICO '00'

                        MOVE 'A' TO ST_DADOS
                        APPEND ST_DADOS ' ' ST_CODIGO_SINTETICO 
                        GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'S'
                       END
                     END
                     IF IT_AUX EQ 3 BEGIN
                      MOVE (LEFT(ST_COD_DESPESA,7)) TO ST_CODIGO_SINTETICO
                      APPEND ST_CODIGO_SINTETICO '00.00'
                      
                      MOVE 'A' TO ST_DADOS
                      APPEND ST_DADOS ' ' ST_CODIGO_SINTETICO
                      GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'S'
                     END
                     IF IT_AUX EQ 2 BEGIN
                      MOVE (LEFT(ST_COD_DESPESA,4)) TO ST_CODIGO_SINTETICO
                      APPEND ST_CODIGO_SINTETICO '00.00.00'

                      MOVE 'A' TO ST_DADOS
                      APPEND ST_DADOS ' ' ST_CODIGO_SINTETICO
                      GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'S'
                     END
                     IF IT_AUX EQ 1 BEGIN
                      MOVE (LEFT(ST_COD_DESPESA,2)) TO ST_CODIGO_SINTETICO
                      APPEND ST_CODIGO_SINTETICO '0.00.00.00'
                      
                      MOVE 'A' TO ST_DADOS
                      APPEND ST_DADOS ' ' ST_CODIGO_SINTETICO
                      GRAVA_TEMPCALC ST_DADOS CONTACOR.CONTA CONTACOR.MES CONTACOR.VALOR_1 CONTACOR.VALOR_2 'S'
                     END
                    LOOP
                   END
            INDICATE FOUND TRUE
           END
 UNTIL [~FOUND]          
LOOP 

MOVE 1 TO PAGECOUNT
MOVE 0 TO RECCOUNT

//--impressao do relatorio
CABEC ST@PREFEITURA                HEADER.1
[~TODAS!] CABEC ENTIDADE.DESCRICAO HEADER.2
[ TODAS!] BEGIN
 [~SEPARA_CAMARA!] CABEC 'CONSOLIDADO - COM CAMARA' HEADER.2
 [ SEPARA_CAMARA!] CABEC 'CONSOLIDADO - SEM CAMARA' HEADER.2
END
[ SINTETICO!] PRINT 'Sintetico' TO HEADER.3
[~SINTETICO!] PRINT 'Analitico' TO HEADER.3
SYSDATA                            HEADER.4
CABEC ST_PERIODO                   HEADER.5
PRINT PAGECOUNT                 TO HEADER.6
OUTPUT HEADER

INDICATE RESERVA! TRUE

CLEAR TEMPCALC
CONSTRAINT_SET 1
CONSTRAIN TEMPCALC.COD_ENTIDADE EQ ST@CODIGO@ENTIDADE
CONSTRAIN TEMPCALC.EXERCICIO    EQ ST@EXERCICIO@FINANCEIRO
CONSTRAIN TEMPCALC.USUARIO      EQ ST@USUARIO
CONSTRAINED_FIND FIRST TEMPCALC BY INDEX.1
WHILE [FOUND]
 KEYCHECK GOSUB LB_ROTINA_PARADA
 INCREMENT RECCOUNT
 CALC (JN_LENDO + 1)     TO JN_LENDO
 MOVE RECCOUNT           TO JN_IMPRIMINDO
 MOVE PAGECOUNT          TO JN_PAGINA

 GOSUB LB_PULA_PAGINA
 MOVE (LEFT(TEMPCALC.CODIGO,1)) TO ST_QUEBRA

 IF ST_QUEBRA EQ 'X' BEGIN
  CABEC 'TOTAL GERAL '       TOTAL.1
  PRINT TEMPCALC.VALOR_1  TO TOTAL.2
  PRINT TEMPCALC.VALOR_2  TO TOTAL.3
  PRINT TEMPCALC.VALOR_3  TO TOTAL.4
  PRINT TEMPCALC.VALOR_4  TO TOTAL.5
  PRINT TEMPCALC.VALOR_5  TO TOTAL.6
  PRINT TEMPCALC.VALOR_6  TO TOTAL.7
  PRINT TEMPCALC.VALOR_7  TO TOTAL.8

  PRINT (TOTAL.4 - TOTAL.6) TO TOTAL.9
  PRINT (TOTAL.6 - TOTAL.8) TO TOTAL.10
  
  OUTPUT TOTAL
 END
 ELSE BEGIN
  BLANKFORM BODY

  MOVEALL '' TO ST_CONTA ST_COD_DESPESA
  MOVE (MID(TEMPCALC.CODIGO,12,3)) TO ST_COD_DESPESA
  
  CLEAR ECONORCA
  MOVE PARGERAL.ESTADO    TO ECONORCA.UF
  MOVE PARGERAL.EXERCICIO TO ECONORCA.ANO
  MOVE ST_COD_DESPESA     TO ECONORCA.CODIGO
  FIND EQ ECONORCA BY INDEX.1

  IF ECONORCA.GRAU EQ '1' OUTPUT BODY
  
  [ RESERVA!] IF (LEFT(ECONORCA.CODIGO,1)) EQ '9' BEGIN
                                                   OUTPUT BODY
                                                   INDICATE RESERVA! FALSE
                                                  END

  MOVE '' TO ST_DESCRICAO
  IF ECONORCA.GRAU EQ '1' MOVE ECONORCA.DESCRICAO TO ST_DESCRICAO
  IF ECONORCA.GRAU EQ '2' APPEND ST_DESCRICAO ' ' ECONORCA.DESCRICAO
  IF ECONORCA.GRAU EQ '3' APPEND ST_DESCRICAO '  ' ECONORCA.DESCRICAO
  IF ECONORCA.GRAU EQ '4' APPEND ST_DESCRICAO '   ' ECONORCA.DESCRICAO
  IF ECONORCA.GRAU EQ '5' APPEND ST_DESCRICAO '    ' ECONORCA.DESCRICAO
  IF ECONORCA.GRAU EQ '6' APPEND ST_DESCRICAO '     ' ECONORCA.DESCRICAO
  IF ECONORCA.GRAU EQ '7' APPEND ST_DESCRICAO '      ' ECONORCA.DESCRICAO

  [~SINTETICO!] IF TEMPCALC.AUXILIAR EQ 'A' LOWERCASE ST_DESCRICAO TO ST_DESCRICAO
  [ SINTETICO!] IF ECONORCA.GRAU EQ '3'     LOWERCASE ST_DESCRICAO TO ST_DESCRICAO

  PRINT ST_COD_DESPESA    TO JBD_CODIGO
  PRINT ST_DESCRICAO      TO JBD_DESCRICAO
  PRINT TEMPCALC.VALOR_1  TO JBD_FIXADO_INICIAL
  PRINT TEMPCALC.VALOR_2  TO JBD_CREDITOS
  PRINT TEMPCALC.VALOR_3  TO JBD_FIXADO_FINAL
  PRINT TEMPCALC.VALOR_4  TO JBD_EMPENHADO_NO_PERIODO
  PRINT TEMPCALC.VALOR_5  TO JBD_EMPENHADO_ATUAL
  PRINT TEMPCALC.VALOR_6  TO JBD_LIQUIDADO_NO_PERIODO
  PRINT TEMPCALC.VALOR_7  TO JBD_LIQUIDADO_ATUAL
  
  IF ECONORCA.GRAU LE 4 BEGIN
   PRINT (TEMPCALC.VALOR_3 - TEMPCALC.VALOR_5) TO JBD_SALDO_EMPENHAR
  END
  
  PRINT (TEMPCALC.VALOR_5 - TEMPCALC.VALOR_7) TO JBD_SALDO_LIQUIDAR
  OUTPUT BODY
  GOSUB LB_PULA_PAGINA
 END
 
 INDICATE ERR FALSE
 ON ERROR GOSUB CHECA_GRAVACAO
 REREAD TEMPCALC
 DELETE TEMPCALC
 UNLOCK
 ON ERROR OFF
 CONSTRAINED_FIND NEXT
 END
CONSTRAINT_SET 1 CLEAR

#INCLUDE R00CONFE.INC

BUSCA_CAMINHO_MENU 'R02CONFE' ''
FORMFEED
GRAVAPAG

[~TITULO_R00CONF!] BEGIN
                    ADVERTE 'VERIFIQUE, NO FIM DO RELATORIO, RELACAO DA(S) ENTIDADE(S) SEM ARQUIVO(S) XML REFERENTE AO PERIODO SELECIONADO.'
                   END
ABORT

LB_ROTINA_PARADA:
 CABEC 'GOSTARIA REALMENTE DE SAIR < S/N >?' CONFIRMA.1
 PAGE CONFIRMA
 INKEYCHECK ST_TECLA IN 'SsNn'
 IF ST_TECLA IN 'Ss' BEGIN
                      MOVE 0 TO RECCOUNT
                      LIMPA_PRN
                     END
 
 MOVE 0 TO CURRENT_IMAGE
 APAGA_TEMPCALC
 PAGE TELA
 PAGE MSG
RETURN

KEYPROC KEY.UP
 BACKFIELD
RETURN

KEYPROC KEY.FIELD
IF CURRENT_WINDOW EQ 1 BEGIN
                        MOVE 'CENTIDAD 1 ' TO ST_JUNTA
                        APPEND ST_JUNTA ST@USUARIO ' ' ST@OPCAO ' ' ST@PROGRAMA ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
                        CHAIN WAIT ST_JUNTA EXPORT_FILES
                        MOVE 0 TO CURRENT_IMAGE
                        PAGE TELA
                        IF ENTIDADE.CODIGO NE "" BEGIN
                                                  TRIM ENTIDADE.CODIGO    TO JN_COD_ENTIDADE
                                                  TRIM ENTIDADE.DESCRICAO TO JN_ENTIDADE
                                                 END
                        ELSE ENTAGAIN
                        RETURN
                       END
ENTAGAIN
RETURN

LB_PULA_PAGINA:
 IF LINECOUNT GE 54 BEGIN
  OUTPUT FOOTER
  FORMFEED
  PRINT PAGECOUNT TO HEADER.6
  OUTPUT HEADER
 END
RETURN


#INCLUDE INTEGRID.INC
