/TELA RESIDENT
ÆÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍµ
³                                                                              ³
³                                                                              ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄ BALANCETE DE VERIFICA€ŽO CONTA CORRENTE - SICOM ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                                                                              ³
³Entidade       <F2>: __ _____________________________________________________ ³
³                                                                              ³
³Mˆs / Ano desejado  ->  Inicial: __ / ____  Final: __ / ____                  ³
³                                                                              ³
³Informe um C/C <F2>: __ _____________________________________________________ ³
³<ENTER>p/todos                                                                ³
³                                                                              ³
³Conta Inicial  <F2>: __________________ _____________________________________ ³
³                                                                              ³
³Conta Final    <F2>: __________________ _____________________________________ ³
³                                                                              ³
³                         Somente Anal¡ticas : _ <S/N>                         ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                               <ESC>  Retorna                                 ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/MSG RESIDENT
ÚÄÄÄÄÄÄÄÄÄÄÄÄ BALANCETE DE VERIFICA€ŽO CONTA CORRENTE - SICOM ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³                                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ SOLICITADO ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                                                                            ³
³                                                                            ³
³              Mˆs Inicial : __ / ____   Mˆs Final : __ / ____               ³
³                                                                            ³
³                                                                            ³
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ PROCESSANDO ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³                                                                            ³
³                                                                            ³
³                                                                            ³
³                         Lendo :  __________.                               ³
³                                                                            ³
³                                                                            ³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/CONFIRMA RESIDENT
ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³______________________________________________________________________________³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/fantasma
______________.__
______________.__
______________.__
______________.__
/HEADER
 --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
|                                                                                                                                                                                                                          |
|                                                                         ____________________________________________________________                                                                                     |
|                                                                                                                                                                                                                          |
|                                                                                                                                                                                                                          |
|                                                          BALANCETE DE VERIFICACAO CONTA CORRENTE - PERIODO MES INICIAL __ / ____ MES FINAL __ / ____                                                                     |
|                                                                                                                                                                                                                          |
|                                                                         ____________________________________________________________                                                                                     |
|  DATA   __/__/____                                                      ____________________________________________________________                                                                     Pagina ______.  |
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|CONTA             |CONTA CORRENTE                                                                                                             |D/C|  SALDO ANTERIOR|          DEBITO|         CREDITO|     SALDO ATUAL|D/C|
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
/BODY RESIDENT
|__________________|___________________________________________________________________________________________________________________________| _ |_________,___.__|_________,___.__|_________,___.__|_________,___.__| _ |
/SUBTOTAL RESIDENT
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|                                                                                                                                  Subtotal  ->    |_________,___.__|_________,___.__|_________,___.__|_________,___.__|   |
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
/TOTAL RESIDENT
                                                                                                                                      Total  ->    |_________,___.__|_________,___.__|_________,___.__|_________,___.__|   |
                                                                                                                                                    -----------------------------------------------------------------------
/FOOTER RESIDENT
 --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
/*
#INCLUDE CONAM.LIB
#INCLUDE BUSCAINF.LIB
#INCLUDE ZERAARQ.LIB

DATE DT_DATA

STRING ST_COD_ENTIDADE     2  ST_N_EMPENHO
STRING ST_JUNTA          255  ST_XML               ST_CONTA
STRING ST_ANO_EMPENHO      4  ST_REGIME_DESPESA    ST_MODALIDADE_LICITACAO ST_N_INSCRICAO      ST_DADOS    255
STRING ST_TIPO_EMPENHO        ST_TIPO_INSCRICAO    ST_CONTA_CORRENTE       ST_EXERCICIO      4 ST_MES

INTEGER IT_CONTADOR IT_RECNUM RECCOUNT IT_AUX IT_MES_INICIAL IT_MES_FINAL

NUMBER NB_SALDO_INICIAL NB_SALDO_FINAL NB_TOTAL_SALDO_INICIAL NB_TOTAL_SALDO_FINAL

AUTOPAGE BODY
NAME JBD_CODIGO        JBD_DESCRICAO JBD_NATUREZA_INICIAL
NAME JBD_SALDO_INICIAL JBD_DEBITO    JBD_CREDITO
NAME JBD_SALDO_FINAL   JBD_NATUREZA_FINAL

OPEN ENTIDADE
OPEN PLANO
OPEN TEMPAUDE
ABRE_CONTACOR ST@EXERCICIO@FINANCEIRO CONTACOR

AUTOPAGE TELA
NAME JN_COD_ENTIDADE    JN_ENTIDADE
NAME JN_MES_INICIAL     JN_ANO_INICIAL
NAME JN_MES_FINAL       JN_ANO_FINAL
NAME JN_CC              JN_CONTA_CORRENTE
NAME JN_CONTA_INICIAL   JN_INICIAL
NAME JN_CONTA_FINAL     JN_FINAL
NAME JN_TIPO

AUTOPAGE MSG
NAME JN_MES_SOLICITADO_I JN_ANO_SOLICITADO_I
NAME JN_MES_SOLICITADO_F JN_ANO_SOLICITADO_F
NAME JN_LENDO

AUTOPAGE FANTASMA
NAME JN_SALDO_INICIAL
NAME JN_CREDITO
NAME JN_DEBITO
NAME JN_SALDO_FINAL

MOVE PARGERAL.EXERCICIO    TO ST_EXERCICIO
MOVE PARGERAL.COD_ENTIDADE TO ST_COD_ENTIDADE

PAGE SET MSG       AT 05 01 COLORS IT_COR_TITULO IT_COR_TITULO
PAGE SET TELA      AT  4  0 COLORS IT_COR_TARJA  IT_COR_MOLDURA
PAGE SET CONFIRMA  AT 21 00 COLORS IT_COR_TARJA  IT_COR_MOLDURA

INICIO:
CLEARFORM TELA
PAGE TELA
MOVE PARGERAL.EXERCICIO TO JN_ANO_INICIAL
MOVE PARGERAL.EXERCICIO TO JN_ANO_FINAL

IF PARGERAL.COD_ENTIDADE EQ '01' BEGIN
 REPEAT
  ACCEPT JN_COD_ENTIDADE {AUTOCLEAR}
  [KEY.ESCAPE] ABORT
  IF JN_COD_ENTIDADE EQ '' MENSAGEM 1
  ELSE BEGIN
        COMZEROS JN_COD_ENTIDADE 2
        CLEAR ENTIDADE
        MOVE ST@EXERCICIO@FINANCEIRO TO ENTIDADE.EXERCICIO 
        MOVE JN_COD_ENTIDADE         TO ENTIDADE.CODIGO
        FIND EQ ENTIDADE BY INDEX.1
        [~FOUND] BEGIN
                  ADVERTE 'ENTIDADE NŽO CADASTRADA'
                  CLEARFORM JN_COD_ENTIDADE THRU JN_ENTIDADE
                 END
       END
 UNTIL JN_COD_ENTIDADE NE ''
 MOVE ENTIDADE.DESCRICAO TO JN_ENTIDADE
 END
ELSE BEGIN
 MOVE PARGERAL.COD_ENTIDADE TO JN_COD_ENTIDADE
 CLEAR ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO ENTIDADE.EXERCICIO 
 MOVE JN_COD_ENTIDADE         TO ENTIDADE.CODIGO
 FIND EQ ENTIDADE BY INDEX.1
 [~FOUND] BEGIN
           ADVERTE 'ENTIDADE NŽO CADASTRADA'
           CLEARFORM JN_COD_ENTIDADE THRU JN_ENTIDADE
          END
 MOVE ENTIDADE.DESCRICAO TO JN_ENTIDADE
 END
MOVE JN_COD_ENTIDADE TO ST_COD_ENTIDADE

INDICATE TODAS! FALSE

LB_MES_I:
ACCEPT JN_MES_INICIAL {AUTOCLEAR}
[KEY.ESCAPE] ABORT
IF JN_MES_INICIAL EQ '' GOTO LB_MES_I
COMZEROS JN_MES_INICIAL 2
IF ((JN_MES_INICIAL LE 0) OR (JN_MES_INICIAL GT 13)) BEGIN
 ADVERTE 'MˆS INV LIDO. TENTE NOVAMENTE.'
 CLEARFORM JN_MES_INICIAL
 GOTO LB_MES_I
 END

ANO_2000 JN_ANO_INICIAL {AUTOCLEAR}

LB_MES_F:
ACCEPT JN_MES_FINAL {AUTOCLEAR}
[KEY.ESCAPE] ABORT
IF JN_MES_FINAL EQ '' GOTO LB_MES_F
COMZEROS JN_MES_FINAL 2
IF ((JN_MES_FINAL LE 0) OR (JN_MES_FINAL GT 13)) BEGIN
 ADVERTE 'MˆS INV LIDO. TENTE NOVAMENTE.'
 CLEARFORM JN_MES_FINAL
 GOTO LB_MES_F
 END

MOVE JN_ANO_INICIAL TO JN_ANO_FINAL

INDICATE NO_EXERCICIO! AS JN_ANO_INICIAL EQ PARGERAL.EXERCICIO

LB_CC:
ACCEPT JN_CC {AUTOCLEAR}
[KEY.ESCAPE] ABORT
IF JN_CC NE '' BEGIN
 COMZEROS JN_CC 2
 BUSCA_TBINFTCE 'RELACAO DE CONTAS CORRENTES' JN_CC JN_CONTA_CORRENTE
 [~@OK!] BEGIN
          ADVERTE 'CONTA CORRENTE INEXISTENTE'
          GOTO LB_CC
         END
 END
INDICATE TODOS! AS JN_CC EQ ''

INDICATE OK! TRUE

ACCEPT JN_CONTA_INICIAL {AUTOCLEAR}
[KEY.ESCAPE] ABORT
IF JN_CONTA_INICIAL EQ '' BEGIN
 MOVE '1' TO JN_CONTA_INICIAL
 INDICATE OK! FALSE
 END
PONTUA_CONTA_CONTABIL JN_CONTA_INICIAL 'I'

CLEAR PLANO
MOVE PARGERAL.ESTADO    TO PLANO.UF
MOVE PARGERAL.EXERCICIO TO PLANO.ANO
MOVE JN_CONTA_INICIAL   TO PLANO.CODIGO
FIND EQ PLANO BY INDEX.1
MOVE PLANO.DESCRICAO TO JN_INICIAL

[ OK!] MOVE JN_CONTA_INICIAL TO JN_CONTA_FINAL
ACCEPT JN_CONTA_FINAL {AUTOCLEAR}
IF JN_CONTA_FINAL EQ '' MOVE '99999999999999' TO JN_CONTA_FINAL
[KEY.ESCAPE] ABORT
PONTUA_CONTA_CONTABIL JN_CONTA_FINAL 'F'

CLEAR PLANO
MOVE PARGERAL.ESTADO    TO PLANO.UF
MOVE PARGERAL.EXERCICIO TO PLANO.ANO
MOVE JN_CONTA_FINAL     TO PLANO.CODIGO
FIND EQ PLANO BY INDEX.1
MOVE PLANO.DESCRICAO TO JN_FINAL

MOVE 'N' TO JN_TIPO
REPEAT
 ACCEPT JN_TIPO {AUTOCLEAR,CAPSLOCK}
 [KEY.ESCAPE] ABORT
 IF NOT JN_TIPO IN 'SSNN' CLEARFORM JN_TIPO
UNTIL JN_TIPO NE ''

INDICATE ANALITICAS! AS JN_TIPO IN 'Ss'

MOVE 'BAL.VERIFICACAO C/C-PERIODO ' TO ST_JUNTA
APPEND ST_JUNTA JN_MES_INICIAL '-' JN_ANO_INICIAL ' A ' JN_MES_FINAL '-' JN_ANO_FINAL
ABREREL ST_JUNTA '132C+'

MOVE JN_MES_INICIAL TO JN_MES_SOLICITADO_I
MOVE JN_ANO_INICIAL TO JN_ANO_SOLICITADO_I

MOVE JN_MES_FINAL   TO JN_MES_SOLICITADO_F
MOVE JN_ANO_FINAL   TO JN_ANO_SOLICITADO_F

PAGE TELA
PAGE MSG
GOTOXY 25 0

APAGA_TEMPAUDE ''

MOVE 1 TO PAGECOUNT
MOVE 0 TO RECCOUNT

IF JN_CC NE '' BEGIN
 MOVE JN_CC TO ST_CONTA_CORRENTE
 APPEND ST_CONTA_CORRENTE '-' JN_CONTA_CORRENTE
 END

CABEC ST@PREFEITURA          HEADER.1
PRINT JN_MES_INICIAL      TO HEADER.2
PRINT JN_ANO_INICIAL      TO HEADER.3
PRINT JN_MES_FINAL        TO HEADER.4
PRINT JN_ANO_FINAL        TO HEADER.5
CABEC JN_ENTIDADE            HEADER.6
SYSDATA                      HEADER.7
PRINT PAGECOUNT           TO HEADER.9
IF JN_CC EQ '' BLANKFORM     HEADER.8
ELSE CABEC ST_CONTA_CORRENTE HEADER.8
OUTPUT HEADER

MOVEALL 0 TO NB_TOTAL_SALDO_INICIAL NB_TOTAL_SALDO_FINAL

MOVE JN_MES_INICIAL TO IT_MES_INICIAL
MOVE JN_MES_FINAL   TO IT_MES_FINAL

CLEAR PLANO
CONSTRAINT_SET 1
CONSTRAIN PLANO.UF     EQ PARGERAL.ESTADO
CONSTRAIN PLANO.ANO    EQ PARGERAL.EXERCICIO
CONSTRAIN PLANO.CODIGO GE JN_CONTA_INICIAL
CONSTRAIN PLANO.CODIGO LE JN_CONTA_FINAL

[ ANALITICAS!] CONSTRAIN PLANO.TIPO           EQ 'A'
[     ~TODOS!] CONSTRAIN PLANO.CONTA_CORRENTE EQ JN_CC

CONSTRAIN PLANO.GRAU   LE '7'
CONSTRAINED_FIND FIRST PLANO BY INDEX.1
WHILE [FOUND]
 CALC (JN_LENDO + 1) TO JN_LENDO
 KEYCHECK GOSUB LB_ROTINA_PARADA

 MOVE PLANO.CONTA_CORRENTE TO ST_XML
 MOVE PLANO.CODIGO         TO ST_CONTA

 INDICATE  IMPRIME! FALSE
 
 MOVE 0 TO IT_AUX
  
 FOR IT_AUX FROM IT_MES_INICIAL TO JN_MES_FINAL
  MOVE IT_AUX TO ST_MES
  COMZEROS ST_MES 2

  CLEAR CONTACOR
  CONSTRAINT_SET 2
  CONSTRAIN CONTACOR.ANO          EQ JN_ANO_INICIAL
  CONSTRAIN CONTACOR.MES          EQ ST_MES
  CONSTRAIN CONTACOR.COD_ENTIDADE EQ ST_COD_ENTIDADE
  CONSTRAIN CONTACOR.XML          EQ ST_XML
  CONSTRAIN CONTACOR.CONTA        EQ ST_CONTA
  CONSTRAINED_FIND FIRST CONTACOR BY INDEX.2
  WHILE [FOUND]
   CALC (JN_LENDO + 1) TO JN_LENDO

   MOVE CONTACOR.DADOS TO ST_DADOS

   CLEAR TEMPAUDE
   MOVE ST@CODIGO@ENTIDADE      TO TEMPAUDE.COD_ENTIDADE
   MOVE ST@EXERCICIO@FINANCEIRO TO TEMPAUDE.EXERCICIO
   MOVE ST@USUARIO              TO TEMPAUDE.USUARIO
   MOVE ST_DADOS                TO TEMPAUDE.CODIGO
   MOVE 'CONFERE'               TO TEMPAUDE.NUMERO
   FIND EQ TEMPAUDE BY INDEX.1
   INDICATE ERR FALSE
   ON ERROR GOSUB CHECA_GRAVACAO
   [ FOUND] REREAD TEMPAUDE

   //--saldo inicial
   IF CONTACOR.MES EQ JN_MES_INICIAL BEGIN
                                      MOVE CONTACOR.NAT_INICIAL                             TO TEMPAUDE.AUXILIAR_1
                                      IF TEMPAUDE.AUXILIAR_2 EQ '' MOVE CONTACOR.NAT_FINAL  TO TEMPAUDE.AUXILIAR_2
                                      CALC (TEMPAUDE.VALOR + CONTACOR.VALOR)                TO TEMPAUDE.VALOR
                                     END
           
   //--movimentacao do periodo
   CALC (TEMPAUDE.VALOR_1 + CONTACOR.VALOR_1) TO TEMPAUDE.VALOR_1   //--DEBITOS
   CALC (TEMPAUDE.VALOR_2 + CONTACOR.VALOR_2) TO TEMPAUDE.VALOR_2   //--CREDITOS
   
   IF TEMPAUDE.AUXILIAR_1 EQ '' MOVE PLANO.SALDO_DC TO TEMPAUDE.AUXILIAR_1
   IF TEMPAUDE.AUXILIAR_2 EQ '' MOVE PLANO.SALDO_DC TO TEMPAUDE.AUXILIAR_2
  
   //--calculo do saldo final
   IF CONTACOR.MES EQ JN_MES_FINAL BEGIN
                                    IF TEMPAUDE.AUXILIAR_1 EQ '' MOVE CONTACOR.NAT_INICIAL TO TEMPAUDE.AUXILIAR_1
                                    MOVE CONTACOR.NAT_FINAL                                TO TEMPAUDE.AUXILIAR_2
                                    CALC (TEMPAUDE.VALOR_3 + CONTACOR.VALOR_3)             TO TEMPAUDE.VALOR_3
                                   END
   SAVERECORD TEMPAUDE
   UNLOCK
   ON ERROR OFF

   CONSTRAINED_FIND NEXT
   END
  CONSTRAINT_SET 2 CLEAR
 LOOP

 MOVEALL 0 TO NB_SALDO_INICIAL NB_SALDO_FINAL

 CLEAR TEMPAUDE
 CONSTRAINT_SET 3
 CONSTRAIN TEMPAUDE.COD_ENTIDADE EQ ST@CODIGO@ENTIDADE
 CONSTRAIN TEMPAUDE.EXERCICIO    EQ ST@EXERCICIO@FINANCEIRO 
 CONSTRAIN TEMPAUDE.USUARIO      EQ ST@USUARIO
 CONSTRAIN TEMPAUDE.NUMERO       EQ 'CONFERE'
 CONSTRAINED_FIND FIRST TEMPAUDE BY INDEX.3
 WHILE [FOUND]
  CALC (JN_LENDO + 1) TO JN_LENDO

  PRINT (MID(TEMPAUDE.CODIGO,18,1))   TO JBD_CODIGO
  PRINT (MID(TEMPAUDE.CODIGO,182,20)) TO JBD_DESCRICAO
  PRINT TEMPAUDE.AUXILIAR_1           TO JBD_NATUREZA_INICIAL
  PRINT TEMPAUDE.VALOR                TO JBD_SALDO_INICIAL
  PRINT TEMPAUDE.VALOR_1              TO JBD_DEBITO
  PRINT TEMPAUDE.VALOR_2              TO JBD_CREDITO
  PRINT TEMPAUDE.VALOR_3              TO JBD_SALDO_FINAL
  PRINT TEMPAUDE.AUXILIAR_2           TO JBD_NATUREZA_FINAL
  
  IF JBD_SALDO_INICIAL LT 0 BEGIN
                             PRINT (JBD_SALDO_INICIAL * (-1)) TO JBD_SALDO_INICIAL
                             CALC  (TEMPAUDE.VALOR    * (-1)) TO TEMPAUDE.VALOR
                            END
  IF JBD_SALDO_FINAL   LT 0 BEGIN
                             PRINT (JBD_SALDO_FINAL   * (-1)) TO JBD_SALDO_FINAL
                             CALC  (TEMPAUDE.VALOR_3  * (-1)) TO TEMPAUDE.VALOR_3
                            END

  GOSUB LB_PULA_PAGINA
  
              INDICATE IMPRIME! AS JBD_SALDO_INICIAL NE 0
  [~IMPRIME!] INDICATE IMPRIME! AS JBD_SALDO_FINAL   NE 0
  [~IMPRIME!] INDICATE IMPRIME! AS JBD_DEBITO        NE 0
  [~IMPRIME!] INDICATE IMPRIME! AS JBD_CREDITO       NE 0
  [ IMPRIME!] BEGIN
               OUTPUT BODY
               INCREMENT RECCOUNT
              END

  IF PLANO.SALDO_DC EQ 'D' BEGIN
   IF TEMPAUDE.AUXILIAR_1 EQ 'D' CALC (NB_SALDO_INICIAL + TEMPAUDE.VALOR)   TO NB_SALDO_INICIAL
   ELSE                          CALC (NB_SALDO_INICIAL - TEMPAUDE.VALOR)   TO NB_SALDO_INICIAL
   IF TEMPAUDE.AUXILIAR_2 EQ 'D' CALC (NB_SALDO_FINAL   + TEMPAUDE.VALOR_3) TO NB_SALDO_FINAL
   ELSE                          CALC (NB_SALDO_FINAL   - TEMPAUDE.VALOR_3) TO NB_SALDO_FINAL
   END
  ELSE BEGIN
   IF TEMPAUDE.AUXILIAR_1 EQ 'C' CALC (NB_SALDO_INICIAL + TEMPAUDE.VALOR)   TO NB_SALDO_INICIAL
   ELSE                          CALC (NB_SALDO_INICIAL - TEMPAUDE.VALOR)   TO NB_SALDO_INICIAL
   IF TEMPAUDE.AUXILIAR_2 EQ 'C' CALC (NB_SALDO_FINAL   + TEMPAUDE.VALOR_3) TO NB_SALDO_FINAL
   ELSE                          CALC (NB_SALDO_FINAL   - TEMPAUDE.VALOR_3) TO NB_SALDO_FINAL
   END

  INDICATE ERR FALSE
  ON ERROR GOSUB CHECA_GRAVACAO
  REREAD TEMPAUDE
  DELETE TEMPAUDE
  UNLOCK
  ON ERROR OFF

  CONSTRAINED_FIND NEXT
  END
 CONSTRAINT_SET 3 CLEAR
 
 IF PLANO.SALDO_DC EQ 'D' BEGIN
  CALC (NB_TOTAL_SALDO_INICIAL + NB_SALDO_INICIAL) TO NB_TOTAL_SALDO_INICIAL
  CALC (NB_TOTAL_SALDO_FINAL   + NB_SALDO_FINAL)   TO NB_TOTAL_SALDO_FINAL
  END
 ELSE BEGIN
  CALC (NB_TOTAL_SALDO_INICIAL - NB_SALDO_INICIAL) TO NB_TOTAL_SALDO_INICIAL
  CALC (NB_TOTAL_SALDO_FINAL   - NB_SALDO_FINAL)   TO NB_TOTAL_SALDO_FINAL
  END

 GOSUB LB_PULA_PAGINA
 PRINT NB_SALDO_INICIAL     TO SUBTOTAL.1
 SUBTOTAL JBD_DEBITO        TO SUBTOTAL.2
 SUBTOTAL JBD_CREDITO       TO SUBTOTAL.3
 PRINT NB_SALDO_FINAL       TO SUBTOTAL.4

             INDICATE IMPRIME! AS SUBTOTAL.1 NE 0
 [~IMPRIME!] INDICATE IMPRIME! AS SUBTOTAL.2 NE 0
 [~IMPRIME!] INDICATE IMPRIME! AS SUBTOTAL.3 NE 0
 [~IMPRIME!] INDICATE IMPRIME! AS SUBTOTAL.4 NE 0
 [ IMPRIME!] OUTPUT SUBTOTAL

 CONSTRAINT_SET 1
 CONSTRAINED_FIND NEXT
 END
CONSTRAINT_SET 1 CLEAR


IF NB_TOTAL_SALDO_INICIAL LT 0 CALC (NB_TOTAL_SALDO_INICIAL * (-1)) TO NB_TOTAL_SALDO_INICIAL
IF NB_TOTAL_SALDO_FINAL   LT 0 CALC (NB_TOTAL_SALDO_FINAL   * (-1)) TO NB_TOTAL_SALDO_FINAL

GOSUB LB_PULA_PAGINA
PRINT NB_TOTAL_SALDO_INICIAL TO TOTAL.1
SUBTOTAL SUBTOTAL.2          TO TOTAL.2
SUBTOTAL SUBTOTAL.3          TO TOTAL.3
PRINT NB_TOTAL_SALDO_FINAL   TO TOTAL.4
OUTPUT TOTAL

IF RECCOUNT LE 0 BEGIN
                  SCREENMODE IT_COR_TITULO ON
                  GOTOXY 22 02
                  SHOW ' RELAT¢RIO NŽO IMPRESSO ! REGISTRO NŽO ENCONTRADO . TECLE <ENTER> PARA SAIR '
                  GOTOXY 25 0
                  INKEY ST_TECLA

                  INDICATE TITULO_R00CONF! TRUE
                 END
ELSE BEGIN
      #INCLUDE R00CONFE.INC
     END

BUSCA_CAMINHO_MENU 'BSICOM02' ''
FORMFEED
GRAVAPAG
APAGA_TEMPAUDE ''

[~TITULO_R00CONF!] BEGIN
                    ADVERTE 'VERIFIQUE, NO FIM DO RELATORIO, RELACAO DA(S) ENTIDADE(S) SEM ARQUIVO(S) XML REFERENTE AO PERIODO SELECIONADO.'
                   END

ABORT

LB_ROTINA_PARADA:
 CABEC 'GOSTARIA REALMENTE DE SAIR < S/N >?' CONFIRMA.1
 PAGE CONFIRMA
 INKEYCHECK ST_TECLA IN 'SsNn'
 IF ST_TECLA IN 'Ss' BEGIN
                      MOVE 0 TO RECCOUNT
                      LIMPA_PRN
                     END

 MOVE 0 TO CURRENT_IMAGE
 PAGE TELA
 PAGE MSG
RETURN

KEYPROC KEY.UP
 BACKFIELD
RETURN

KEYPROC KEY.FIELD
IF CURRENT_WINDOW EQ 1 BEGIN
                        MOVE 'CENTIDAD 1 ' TO ST_JUNTA
                        APPEND ST_JUNTA ST@USUARIO ' ' ST@OPCAO ' ' ST@PROGRAMA ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
                        CHAIN WAIT ST_JUNTA EXPORT_FILES
                        MOVE 0 TO CURRENT_IMAGE
                        PAGE TELA
                        IF ENTIDADE.CODIGO NE "" BEGIN
                                                  TRIM ENTIDADE.CODIGO    TO JN_COD_ENTIDADE
                                                  TRIM ENTIDADE.DESCRICAO TO JN_ENTIDADE
                                                 END
                        ELSE ENTAGAIN
                        RETURN
                       END
IF CURRENT_WINDOW EQ 7 BEGIN
                        MOVE 'CTBINFTC 1 ' TO ST_JUNTA
                        APPEND ST_JUNTA ST@USUARIO ' ' ST@OPCAO ' ' ST@PROGRAMA ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO ' ' 'RELACAO@DE@CONTAS@CORRENTES'
                        CHAIN WAIT ST_JUNTA EXPORT_FILES
                        MOVE 0 TO CURRENT_IMAGE
                        PAGE TELA
                        IF TBINFTCE.CODIGO NE '' MOVE TBINFTCE.CODIGO TO JN_CC
                        ELSE ENTAGAIN
                        RETURN
                       END
IF ((CURRENT_WINDOW EQ 9) OR (CURRENT_WINDOW EQ 11)) BEGIN
                                                      MOVE 'CCONTAS2 1 ' TO ST_JUNTA
                                                      APPEND ST_JUNTA ST@USUARIO ' ' ST@OPCAO ' ' ST@PROGRAMA ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO ' ' JN_ANO_INICIAL
                                                      CHAIN WAIT ST_JUNTA EXPORT_FILES
                                                      IF PLANO.CODIGO NE '' BEGIN
                                                                             IF CURRENT_WINDOW EQ 9 MOVE PLANO.CODIGO TO JN_CONTA_INICIAL
                                                                             ELSE                   MOVE PLANO.CODIGO TO JN_CONTA_FINAL
                                                                            END
                                                      MOVE 0 TO CURRENT_IMAGE
                                                      PAGE TELA
                                                      RETURN
                                                     END

ENTAGAIN
RETURN

LB_PULA_PAGINA:
 IF LINECOUNT GE 54 BEGIN
  OUTPUT FOOTER
  FORMFEED
  PRINT PAGECOUNT TO HEADER.9
  OUTPUT HEADER
  END
RETURN



#INCLUDE INTEGRID.INC




