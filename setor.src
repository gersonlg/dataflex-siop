/TELA RESIDENT
Ё __/__/____  ________________________________________________________         Ё
фмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╣
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё ______    ______  _____                                                      Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё Descri┤└o       : __________________________________________________         Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё Sigla           : __________                                                 Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё     <F1> ajuda     <F3> ёltimo __________     <F9> limpa    <ESC> retorno    Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/CONFIRMA RESIDENT
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё______________________________________________________________________________Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/ULTIMO_CADASTRADO RESIDENT
здддддддддддддддддддддддддддддддддддддддддддддд©
Ё                                              Ё
ЁO ёltimo Setor __________ foi:                Ё
Ё                                              Ё
Ё C╒digo    _____                              Ё
Ё Descri┤└o ________________________________   Ё
Ё Sigla     __________                         Ё
Ё                                              Ё
Ё                                              Ё
Ё Pressione qualquer tecla para continuar.     Ё
юдддддддддддддддддддддддддддддддддддддддддддддды
/INCLUSAO RESIDENT
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё                      ESTA ░ A INCLUS▌O DO SETOR:                       Ё
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё                                _____                                   Ё
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё                Pressione qualquer tecla para continuar                 Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/*
#INCLUDE CONAM.LIB

STRING ST_DESTINO

AUTOPAGE INCLUSAO
NAME JN_CODIGO

AUTOPAGE TELA
NAME JN_TARJA_DATA
NAME JN_TARJA_TITULO
NAME JN_TITULO    JN_F2    JN_N_SETOR
NAME JN_DESCRICAO JN_SIGLA
NAME JN_F3

OPEN SETOR
OPEN MENUUSER

PAGE SET CONFIRMA          AT 21 00 COLORS IT_COR_TARJA  IT_COR_MOLDURA
PAGE SET TELA              AT 03 00 COLORS IT_COR_TARJA  IT_COR_MOLDURA
PAGE SET ULTIMO_CADASTRADO AT 06 18 COLORS IT_COR_TITULO IT_COR_TITULO
PAGE SET INCLUSAO          AT 16 03 COLORS IT_COR_TITULO IT_COR_TITULO

SCREENMODE IT_COR_MOLDURA ON
SYSDATA JN_TARJA_DATA 
SCREENMODE IT_COR_TITULO ON
IF ST@OPCAO EQ '1' BEGIN
                    CABEC 'INCLUS▌O DE SETOR' JN_TARJA_TITULO
                    MOVE 'Inclu║do' TO ULTIMO_CADASTRADO.1
                   END
IF ST@OPCAO EQ '2' BEGIN
                    CABEC 'ALTERA─▌O DE SETOR' JN_TARJA_TITULO
                    MOVE 'Alterado' TO ULTIMO_CADASTRADO.1
                   END
IF ST@OPCAO EQ '3' BEGIN
                    CABEC 'EXCLUS▌O DE SETOR' JN_TARJA_TITULO
                    MOVE 'Exclu║do' TO ULTIMO_CADASTRADO.1
                   END
SCREENMODE IT_COR_MOLDURA ON
MOVE 'C╒digo' TO JN_TITULO
MOVE '<F2> :'   TO JN_F2
IF ST@OPCAO EQ '1' MOVE 'cadastrado' TO JN_F3
IF ST@OPCAO EQ '2' MOVE 'alterado  ' TO JN_F3
IF ST@OPCAO EQ '3' MOVE 'exclu║do  ' TO JN_F3
SCREENMODE IT_COR_TARJA ON

INICIO:
CLEARFORM JN_N_SETOR THRU JN_SIGLA
INDICATE ALTERA! TRUE

IF ST@OPCAO EQ '1' BEGIN
                    SCREENMODE IT_COR_MOLDURA ON
                    BLANKFORM JN_TITULO THRU JN_N_SETOR
                    SCREENMODE IT_COR_TARJA ON
                   END
ELSE BEGIN
      REPEAT
       ACCEPT JN_N_SETOR {AUTOCLEAR}
       IF JN_N_SETOR EQ '' MENSAGEM 1
       ELSE BEGIN
             COMZEROS JN_N_SETOR 5

             CLEAR SETOR
             MOVE ST@CODIGO@ENTIDADE      TO SETOR.COD_ENTIDADE
             MOVE ST@EXERCICIO@FINANCEIRO TO SETOR.EXERCICIO
             MOVE JN_N_SETOR              TO SETOR.CODIGO
             FIND EQ SETOR BY INDEX.1
             [~FOUND] BEGIN
                       ADVERTE 'SETOR N▌O CADASTRADO'
                       CLEARFORM JN_N_SETOR
                      END
            END
      UNTIL JN_N_SETOR NE ''
      MOVE SETOR.DESCRICAO  TO JN_DESCRICAO
      MOVE SETOR.SIGLA      TO JN_SIGLA
     END

IF ST@OPCAO NE '3' BEGIN
 LB_INICIO:
 ACCEPT JN_DESCRICAO {CAPSLOCK,AUTOCLEAR}
 IF JN_DESCRICAO EQ '' BEGIN
                        DISPENSA 1
                       END

 REPEAT
  ACCEPT  JN_SIGLA {CAPSLOCK,AUTOCLEAR}
  IF JN_SIGLA EQ '' MENSAGEM 1
 UNTIL JN_SIGLA NE ''
 END
ELSE BEGIN
 INDICATE OK! TRUE
 CLEAR MENUUSER
 CONSTRAINT_SET 1
 CONSTRAIN MENUUSER.N_SETOR EQ JN_N_SETOR
 CONSTRAINED_FIND FIRST MENUUSER BY INDEX.1
 WHILE [FOUND]
  INDICATE OK! FALSE
  CONSTRAINED_FIND NEXT
  END
 CONSTRAINT_SET 1 CLEAR

 [~OK!] BEGIN
         ADVERTE 'SETOR J═ UTILIZADO NO CADASTRO DE USU═RIO, IMPOSS║VEL EXCLUIR'
         GOTO INICIO
        END
 END

IF ST@OPCAO EQ '1' CONFIRMACAO 'INCLUS▌O'
IF ST@OPCAO EQ '2' CONFIRMACAO 'ALTERA─▌O'
IF ST@OPCAO EQ '3' CONFIRMACAO 'EXCLUS▌O'

INDICATE ERR FALSE
ON ERROR GOSUB CHECA_GRAVACAO
REREAD
 IF ST@OPCAO NE '1' BEGIN
                     CLEAR SETOR
                     MOVE ST@CODIGO@ENTIDADE      TO SETOR.COD_ENTIDADE
                     MOVE ST@EXERCICIO@FINANCEIRO TO SETOR.EXERCICIO
                     MOVE JN_N_SETOR              TO SETOR.CODIGO
                     FIND EQ SETOR BY INDEX.1
                     [~FOUND] BEGIN
                               UNLOCK
                               ON ERROR OFF
                               DISPENSA 358
                              END
                    END

 ELSE BEGIN
       CLEAR SETOR
       MOVE ST@CODIGO@ENTIDADE      TO SETOR.COD_ENTIDADE
       MOVE ST@EXERCICIO@FINANCEIRO TO SETOR.EXERCICIO
       MOVE '99999'                 TO SETOR.CODIGO
       FIND LT SETOR BY INDEX.1
       [ FOUND] INDICATE FOUND AS SETOR.COD_ENTIDADE EQ ST@CODIGO@ENTIDADE
       [ FOUND] INDICATE FOUND AS SETOR.EXERCICIO    EQ ST@EXERCICIO@FINANCEIRO
       [ FOUND] CALC (SETOR.CODIGO + 1) TO JN_CODIGO
       [~FOUND] MOVE '1'                TO JN_CODIGO
       COMZEROS JN_CODIGO 5

       CLEAR SETOR
       MOVE ST@CODIGO@ENTIDADE      TO SETOR.COD_ENTIDADE
       MOVE ST@EXERCICIO@FINANCEIRO TO SETOR.EXERCICIO
       MOVE JN_CODIGO               TO SETOR.CODIGO
      END

 IF ST@OPCAO NE '3' BEGIN
                     MOVE JN_DESCRICAO TO SETOR.DESCRICAO
                     MOVE JN_SIGLA     TO SETOR.SIGLA
                     SAVERECORD SETOR
                    END
 ELSE BEGIN
       DELETE SETOR
      END

IF ST@OPCAO EQ '1' GRAVAINCLUSAO 'SETOR' JN_CODIGO 'I'
IF ST@OPCAO EQ '2' GRAVAINCLUSAO 'SETOR' JN_N_SETOR 'A'
IF ST@OPCAO EQ '3' GRAVAINCLUSAO 'SETOR' JN_N_SETOR 'E'
UNLOCK
ON ERROR OFF

MOVE SETOR.CODIGO  TO ULTIMO_CADASTRADO.2
MOVE JN_DESCRICAO  TO ULTIMO_CADASTRADO.3
MOVE JN_SIGLA      TO ULTIMO_CADASTRADO.4

IF ST@OPCAO EQ '1' BEGIN
                    BEEP
                    PAGE INCLUSAO
                    GOTOXY 30 00
                    PAUSE
                   END

GOTO INICIO
DESATIVA

KEYPROC KEY.ESCAPE
CONFIRMACAO 'RETORNO'

KEYPROC KEY.SFIND            
ULT_CADASTRADO 

KEYPROC KEY.HELP
AJUDA
RETURN

KEYPROC KEY.CLEAR
RETURN INICIO

KEYPROC KEY.UP
IF CURRENT_WINDOW EQ 6 BEGIN
                        IF ST@OPCAO EQ '1' RETURN LB_INICIO
                        ELSE BACKFIELD
                       END
ELSE BACKFIELD
RETURN

KEYPROC KEY.FIELD
 IF CURRENT_WINDOW EQ 5 BEGIN
                         MOVE 'CSETOR 1 ' TO ST_DESTINO
                         APPEND ST_DESTINO ST@USUARIO ' ' ST@OPCAO ' ' ST@PROGRAMA ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
                         CHAIN WAIT ST_DESTINO EXPORT_FILES
                         MOVE 0 TO CURRENT_IMAGE
                         PAGE TELA
                         IF SETOR.CODIGO NE '' BEGIN
                                                TRIM SETOR.CODIGO    TO JN_N_SETOR
                                                TRIM SETOR.DESCRICAO TO JN_DESCRICAO
                                               END
                         ELSE ENTAGAIN
                        END
 ELSE ENTAGAIN
RETURN

#INCLUDE INTEGRID.INC


