/TELA RESIDENT
зд SIOPE - SIOPS дддддддддддддддддддддддддддддддддддддддддддддддддддддддд ____ ©
Ё______________________________________________________________________________Ё
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё __/__/____  ________________________________________________________         Ё
фмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╣
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
Ё                                                                              Ё
цддддддддддддддддддддддддддд_____________________дддддддддддддддддддддддддддддд╢
Ё______________________________________________________________________________Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/TIT RESIDENT
________________________________________________________
/ULTIMOS_MENUS RESIDENT
____________ ____________ ____________ ____________ ____________
____________ ____________ ____________ ____________ ____________
____________ ____________ ____________ ____________ ____________
/ULTIMAS_OPCOES RESIDENT
_ _ _ _ _ _ _ _ _ _ _ _ _ _ _
/ULTIMO_PROGRAMA RESIDENT
____________ ____________ ____________ ____________
____________ ____________ ____________
/ULTIMO_TIPO RESIDENT
_ _ _ _ _ _ _
/ULTIMA_SENHA RESIDENT
_. _. _. _. _. _. _.
/OPCOES_1  RESIDENT
зддддддддддддддддддддддд©
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
юддддддддддддддддддддддды
/OPCOES_2  RESIDENT
зддддддддддддддддддддддд©
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
юддддддддддддддддддддддды
/OPCOES_3  RESIDENT
зддддддддддддддддддддддд©
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
Ё                       Ё
Ё_______________________Ё
юддддддддддддддддддддддды
/OPCOES_4  RESIDENT
зддддддддддддддддддддддд©
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
Ё                       Ё
юддддддддддддддддддддддды
/USUARIO_ATIVO  RESIDENT
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё                                                                            Ё
Ё                             A T E N C A O   !!!                            Ё
Ё                                                                            Ё
Ё                                                                            Ё
Ё    USUARIO JA ESTA UTILIZANDO ESTE PROGRAMA EM OUTRO TERMINAL OU OCORREU   Ё
Ё    ALGUMA INTERRUPCAO ANORMAL.                                             Ё
Ё                                                                            Ё
Ё    ACESSANDO ESTE PROGRAMA COM O MESMO USUARIO EM MAIS DE UM TERMINAL, A   Ё
Ё    GRAVACAO DOS REGISTROS PODERA SER PREJUDICADA DEVIDO A UTILIZACAO  DE   Ё
Ё    ARQUIVOS TEMPORARIOS QUE TEM COMO CHAVE DE ACESSO O CODIGO DO USUARIO.  Ё
Ё                                                                            Ё
Ё    TECLE <S> PARA CONTINUAR, CASO CONTRARIO <N> PARA RETORNAR AO MENU.     Ё
Ё                                                                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/USUARIO_ATIVO_2  RESIDENT
здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё                                                                            Ё
Ё                                                                            Ё
Ё                             A T E N C A O   !!!                            Ё
Ё                                                                            Ё
Ё                                                                            Ё
Ё                                                                            Ё
Ё    USUARIO JA ESTA UTILIZANDO ESTE PROGRAMA EM OUTRO TERMINAL OU OCORREU   Ё
Ё    ALGUMA INTERRUPCAO ANORMAL.                                             Ё
Ё                                                                            Ё
Ё                                                                            Ё
Ё    PRESSIONE QUALQUER TECLA PARA CONTINUAR.                                Ё
Ё                                                                            Ё
Ё                                                                            Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/CONFIRMA RESIDENT
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё______________________________________________________________________________Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/SOMBRA
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
/RODAPE RESIDENT
цдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё______________________________________________________________________________Ё
юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
/TELA_ESPACO RESIDENT
зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
Ё                  PROBLEMAS DE ESPA─O EM DISCO                       Ё
цддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢
Ё                                                                     Ё
Ё                                                                     Ё
Ё                                                                     Ё
Ё FOI VERIFICADO QUE O ESPA─O EM DISCO NO SERVIDOR DE DADOS EST═ __%  Ё
Ё                                                                     Ё
Ё EM USO, PODENDO COMPROMETER AS GRAVA─≥ES DE DADOS DO SISTEMA.       Ё
Ё                                                                     Ё
Ё                                                                     Ё
Ё                                                                     Ё
Ё                                                                     Ё
юдддддддддддддд Pressione Qualquer Tecla para SAIR ддддддддддддддддддды
/*
STRING ST@CODIGO@ENTIDADE@MAIN ST@EXERCICIO@FINANCEIRO@MAIN ST@NOVO@TECNICO
STRING ST@SISTEMA              ST@USUARIO@MAIN              ST@CODIGO@PREF
STRING ST_DIRETORIO        255 ST_DESTINO               255 ST_CABECALHO
STRING ST_GUARDA_PROGRAMA      ST_VOL                       ST@TECNICO
STRING ST_EXERCICIO            ST_NOME_RESOLVIDO            ST_SEQUENCIA

CMDLINE ST_CABECALHO
CMDLINE ST@USUARIO@MAIN
CMDLINE ST@CODIGO@ENTIDADE@MAIN
CMDLINE ST@EXERCICIO@FINANCEIRO@MAIN

// clearxy 0 0
// SHOWLN  'MAIN      '
// SHOWLN  'titulo    ' ST_CABECALHO
// SHOWLN  'volume    ' ST_VOL
// SHOWLN  'tecnico   ' ST@NOVO@TECNICO
// SHOWLN  'cod pre   ' ST@CODIGO@PREF
// SHOWLN  'sistema   ' ST@SISTEMA
// SHOWLN  'user      ' ST@USUARIO@MAIN
// SHOWLN  'entidade  ' ST@CODIGO@ENTIDADE@MAIN
// SHOWLN  'exercicio ' ST@EXERCICIO@FINANCEIRO@MAIN
// pause .

#INCLUDE CONAM.LIB

versao

SET_OPTION NO_CALC

COMBRANCOS ST@USUARIO@MAIN 5

//--main unico
MOVE ST@USUARIO@MAIN              TO ST@USUARIO
MOVE ST@CODIGO@ENTIDADE@MAIN      TO ST@CODIGO@ENTIDADE
MOVE ST@EXERCICIO@FINANCEIRO@MAIN TO ST@EXERCICIO@FINANCEIRO

IF ST@USUARIO@MAIN EQ '' BEGIN
                          MOVE 'LOGIN' TO ST_DESTINO
                          CHAIN ST_DESTINO
                         END

CLEAR MENUUSER
//--

INDICATE ENTROU! TRUE

STRING ST_TERMINAL_ATUAL

DATE    DT_DATA_SISTEMA DT_DATA
INTEGER IT_DATA         IT_TAMANHO         IT_CONTADOR    IT_SIS
INTEGER IT_PIDATUAL     IT_RECNUM_MENUTITU

SYSDATE4 DT_DATA_SISTEMA

//--
//-- sfpm multi entidade nome cliente do pargeral
//--
MOVE PARGERAL.NOME_ENTIDADE                             TO ST_CABECALHO
MOVE (REPLACES(' ',(TRIM(PARGERAL.NOME_ENTIDADE)),'@')) TO ST_NOME_RESOLVIDO
MOVE PARGERAL.EXERCICIO                                 TO ST_EXERCICIO

INDICATOR DINAMICO!

STRING  ST_OPCAO ST_NOME_MENU ST_TITULOX

INTEGER  IT_CONTA_OPCOES IT_CONTA_MENUS IT_TENTATIVAS IT_WINDOW
INTEGER  IT_ULTIMA_QTDE  IT_OPCAO IT_DINAMICO IT_GUARDA_WINDOWINDEX

INTEGER         IT_TELA_CORR    IT_AUX
INDICATOR       EXISTE_MENU!    TECLA!          FOLHA_PAGTO!    KEY!
STRING          ST_AUX 25       ST_COMP 7       ST_DESCR 12
STRING          ST_MES 2        ST_ANO 4        ST_MENU 25
STRING          ST_COMP_AAAAA 7 ST_TIPO 1       ST_NOME_WEBSAFE 255

#COMMAND BUSCA_USUARIO
 CLEAR MENUUSER
 MOVE ST@USUARIO TO MENUUSER.CODIGO
 FIND EQ MENUUSER BY INDEX.1
 [ FOUND] BEGIN
           INDICATE ERR FALSE
           ON ERROR GOSUB CHECA_GRAVACAO
           REREAD MENUUSER
          END
#ENDCOMMAND

// este comando movimenta os itens do menu para a tela
// Formato: MOVER_MENU <Numero da Tela 1-3> <Flag de Movimentacao das Opcoes>
#COMMAND MOVER_MENU
 SCREENMODE IT_COR_TARJA ON
 IF !2 EQ 1 MOVE 0 TO IT_CONTA_OPCOES
 FOR FIELDINDEX FROM 0 TO 6
  MOVE FIELDINDEX TO WINDOWINDEX
  MOVE "" TO ST_AUX
  CALC (FIELDINDEX + 1) TO IT_AUX
  IF MENUCOMP.NOME_OP& NE "" APPEND ST_AUX IT_AUX "- " MENUCOMP.NOME_OP&
  IF !1 EQ 1 MOVE ST_AUX TO OPCOES_1.1&
  IF !1 EQ 2 MOVE ST_AUX TO OPCOES_2.1&
  IF !1 EQ 3 MOVE ST_AUX TO OPCOES_3.1&
  IF !2 EQ 1 BEGIN
   MOVE MENUCOMP.PROG_MENU& TO ULTIMO_PROGRAMA.1&
   MOVE MENUCOMP.P_M& TO ULTIMO_TIPO.1&
   MOVE MENUCOMP.QTD_SENHAS& TO ULTIMA_SENHA.1&
   IF MENUCOMP.NOME_OP& NE "" INCREMENT IT_CONTA_OPCOES
   END
 LOOP

 IF !1 EQ 1 PAGE OPCOES_1
 IF !1 EQ 2 PAGE OPCOES_2
 IF !1 EQ 3 PAGE OPCOES_3
#ENDCOMMAND

#COMMAND DISPLAYX %R
 CALC (IT_OPCAO - 1) TO WINDOWINDEX
 IF !1 EQ 1 BEGIN
  PAGE OPCOES_1
  DISPLAY OPCOES_1.1& TO OPCOES_1.1&
  END
 IF !1 EQ 2 BEGIN
  PAGE OPCOES_2
  DISPLAY OPCOES_2.1& TO OPCOES_2.1&
  END
 IF !1 EQ 3 BEGIN
  PAGE OPCOES_3
  DISPLAY OPCOES_3.1& TO OPCOES_3.1&
  END
#ENDCOMMAND

// nomeacao das janelas

AUTOPAGE TELA
NAME JN_EXERCICIO
NAME JN_TARJA
NAME JN_TARJA_DATA JN_TARJA_TITULO JN_MSG
NAME JN_RODAPE_TELA

AUTOPAGE RODAPE
NAME JN_RODAPE_RODAPE

OPEN MENUTITU
OPEN MENUCOMP
OPEN MENUUSER
OPEN MENUACES

FILE_MODE MENUTITU READ_ONLY
FILE_MODE MENUCOMP READ_ONLY

MOVE   0 TO IT_WINDOW
MOVE   0 TO IT_CONTA_MENUS

MUDA_CORES:

GOSUB PEGA_COR

PAGE SET TELA             AT  0  0  COLORS  IT_COR_TARJA   IT_COR_MOLDURA
PAGE SET RODAPE           AT 21 00  COLORS  IT_COR_TARJA   IT_COR_MOLDURA
PAGE SET CONFIRMA         AT 21 00  COLORS  IT_COR_TARJA   IT_COR_MOLDURA
PAGE SET FIM              AT 21 00  COLORS  IT_COR_TARJA   IT_COR_MOLDURA
PAGE SET TIT              AT  3 14  COLORS  IT_COR_TARJA   IT_COR_MOLDURA
PAGE SET OPCOES_1         AT 06 01  COLORS  IT_COR_TARJA   IT_COR_TARJA
PAGE SET OPCOES_2         AT 06 27  COLORS  IT_COR_TARJA   IT_COR_TARJA
PAGE SET OPCOES_3         AT 06 53  COLORS  IT_COR_TARJA   IT_COR_TARJA
PAGE SET OPCOES_4         AT 06 53  COLORS  IT_COR_TARJA   IT_COR_TARJA
PAGE SET SOMBRA           AT 16 06  COLORS  IT_COR_TARJA   IT_COR_TARJA
PAGE SET USUARIO_ATIVO    AT 06 01  COLORS  IT_COR_TARJA   IT_COR_TITULO
PAGE SET USUARIO_ATIVO_2  AT 06 01  COLORS  IT_COR_TARJA   IT_COR_TITULO
PAGE SET TELA_ESPACO      AT 05 05  COLORS  IT_COR_TITULO  IT_COR_TITULO

SCREENMODE IT_COR_MOLDURA ON
MOVE ST_EXERCICIO TO JN_EXERCICIO
MOVE 'ддддддддддддддддддддд' TO JN_MSG
SCREENMODE IT_COR_TARJA ON
SYSDATA JN_TARJA_DATA
SCREENMODE IT_COR_TARJA ON

CABEC ST_CABECALHO JN_TARJA
CABEC '<F1>Ajuda        <F7>Relat╒rio       <F8>Impressora       <ESC>Sai' JN_RODAPE_TELA
CABEC '<F1>Ajuda        <F7>Relat╒rio       <F8>Impressora       <ESC>Sai' JN_RODAPE_RODAPE
SCREENMODE IT_COR_TARJA ON
SYSDATA JN_TARJA_DATA
SCREENMODE IT_COR_TARJA ON

//--seta programa seguranca e nao seguranca
CLEAR MENUUSER
MOVE ST@USUARIO TO MENUUSER.CODIGO
FIND EQ MENUUSER BY INDEX.1

INDICATE MUDA_PGM! TRUE

//--------ver espaco disponivel em disco
STRING  ST_LINHA_ESPACO 255 ST_COMANDO_ESPACO 255 ST_ESPACO_LIVRE
INTEGER IT_POS_ESPACO       ST_LIMITE_ESPACO      ST_VERIFICA_ESPACO_LIVRE

MOVEALL '' TO ST_ESPACO_LIVRE ST_VERIFICA_ESPACO_LIVRE

MOVE (OBTER_CONFIGURACAO('espaco_livre',ST@CODIGO@ENTIDADE,ST@EXERCICIO@FINANCEIRO)) TO ST_VERIFICA_ESPACO_LIVRE

IF ST_VERIFICA_ESPACO_LIVRE NE '' BEGIN
                                   MOVE 'df -h > espaco_livre.egi' TO ST_COMANDO_ESPACO
                                   RUNPROGRAM WAIT ST_COMANDO_ESPACO

                                   DIRECT_INPUT 'espaco_livre.egi'

                                   REPEAT
                                    READLN ST_LINHA_ESPACO
                                    TRIM ST_LINHA_ESPACO TO ST_LINHA_ESPACO
                                    MOVE '' TO ST_ESPACO_LIVRE
                                    
                                    IF (RIGHT(ST_LINHA_ESPACO,6)) EQ '/dados' BEGIN
                                                                               MOVE 0 TO IT_POS_ESPACO
                                                                               POS '/dados' IN ST_LINHA_ESPACO TO IT_POS_ESPACO
                                                                               
                                                                               MOVE (MID(ST_LINHA_ESPACO,5,(IT_POS_ESPACO-5))) TO ST_ESPACO_LIVRE
                                                                               REMOVE '%' FROM ST_ESPACO_LIVRE
                                                                               TRIM ST_ESPACO_LIVRE TO ST_ESPACO_LIVRE
                                                                              END
                                    
                                    IF (RIGHT(ST_LINHA_ESPACO,5)) EQ '/home' BEGIN
                                                                              MOVE 0 TO IT_POS_ESPACO
                                                                              POS '/home' IN ST_LINHA_ESPACO TO IT_POS_ESPACO
                                                                              
                                                                              MOVE (MID(ST_LINHA_ESPACO,5,(IT_POS_ESPACO-5))) TO ST_ESPACO_LIVRE
                                                                              REMOVE '%' FROM ST_ESPACO_LIVRE
                                                                              TRIM ST_ESPACO_LIVRE TO ST_ESPACO_LIVRE
                                                                             END

                                    IF ST_ESPACO_LIVRE GE ST_VERIFICA_ESPACO_LIVRE BEGIN
                                                                                    PAGE TELA_ESPACO
                                                                                    SCREENMODE IT_COR_TITULO ON
                                                                                    PRINT ST_ESPACO_LIVRE TO TELA_ESPACO.1
                                                                                    PAUSE ''
                                                                                    ERASEFILE 'espaco_livre.egi'
                                                                                    ABORT
                                                                                   END
                                   UNTIL [ SEQEOF]
                                   
                                   CLOSE_INPUT
                                  END
//--final ver espaco

PAGE TELA
GOSUB PEGA_COR
PAGE TELA
GOSUB DT

SCREENMODE IT_COR_TITULO ON
BLANKFORM JN_TARJA_TITULO
SCREENMODE IT_COR_TARJA ON

VOLTA_MENU:

INDICATE INICIA! TRUE

PAGE SET FIM AT 21 00 COLORS IT_COR_TARJA IT_COR_MOLDURA
MOVE 1 TO IT_OPCAO
MOVE 0 TO IT_CONTA_MENUS
MOVEALL "" TO ST_COMP ST_TIPO

INDICATE DINAMICO! FALSE
MOVE 0 TO IT_DINAMICO

INI_TELAS:
 MOVE 1 TO IT_TELA_CORR
 CLEAR MENUCOMP
 CLEAR MENUTITU
 MOVE 0 TO WINDOWINDEX
 MOVEALL "SISTEMA" TO ST_NOME_MENU ULTIMOS_MENUS.1& MENUCOMP.NOME MENUTITU.NOME
 FIND EQ MENUCOMP BY INDEX.1
 FIND EQ MENUTITU BY INDEX.1
 GOSUB DT
 PAGE TELA
 MOVER_MENU 1 1
 GOSUB MOSTRA_TITULO
 CLEARFORM OPCOES_3

 [ ENTROU!] BEGIN
             INDICATE ENTROU! FALSE
              
             INDICATE ERR FALSE
             ON ERROR GOSUB CHECA_GRAVACAO
             REREAD MENUUSER
             DATA_DIA MENUUSER.DATA_TRAB
             SAVERECORD MENUUSER
             UNLOCK
             ON ERROR OFF
             
             SCREENMODE IT_COR_MOLDURA ON
             MOVE MENUUSER.DATA_TRAB TO JN_TARJA_DATA
             SCREENMODE IT_COR_TARJA ON
             PAGE TELA
            END

//obtem_acesso
INICIO:
 [ DINAMICO!] BEGIN
  INCREMENT IT_DINAMICO
  IF (MID (ST_SEQUENCIA, 1, IT_DINAMICO)) EQ "" BEGIN
   MOVE 1 TO IT_OPCAO
   INDICATE DINAMICO! FALSE
   END
  ELSE  MOVEALL (MID (ST_SEQUENCIA, 1, IT_DINAMICO)) TO IT_OPCAO ST_TECLA
  END

 CALC (IT_OPCAO - 1) TO WINDOWINDEX
 IF IT_TELA_CORR NE 3 BEGIN
  MOVE ULTIMO_PROGRAMA.1& TO ST_NOME_MENU
  GOSUB LER_OPCOES
  IF IT_TELA_CORR EQ 1 MOVER_MENU 2 0
  IF IT_TELA_CORR EQ 2 MOVER_MENU 3 0
  END
 GOSUB ALTA_INTENSIDADE

REPEAT
 [~DINAMICO!] INKEY ST_TECLA
 GOSUB BAIXA_INTENSIDADE
 INDICATE TECLA! GROUP ANY [KEY.ESCAPE KEY.LEFT]
 [TECLA!] BEGIN
           IF IT_CONTA_MENUS EQ 0 BEGIN
                                   SCREENMODE IT_COR_TITULO ON
                                   CABEC 'GOSTARIA REALMENTE DE SAIR <S/N>' CONFIRMA.1
                                   SCREENMODE IT_COR_TARJA ON
                                   PAGE CONFIRMA
                                   INKEYCHECK ST_TECLA IN 'SsNn'
                                   IF ST_TECLA IN 'sS' BEGIN
                                                        //limpar userpid do usuario/terminal
                                                        GOSUB LB_LIMPA_USERPID
                                                        SCREENMODE IT_COR_TITULO ON
                                                        ABORT
                                                       END
                                   GOTO INI_TELAS
                                  END
           ELSE BEGIN
                 GOSUB VOLTA_TELA
                 GOTO INICIO
                END
          END

 [KEY.UP] BEGIN
  IF IT_OPCAO LE 1 MOVE IT_CONTA_OPCOES TO IT_OPCAO
  ELSE DECREMENT IT_OPCAO
  GOTO INICIO
  END

 [KEY.DOWN] BEGIN
  IF IT_OPCAO GE IT_CONTA_OPCOES MOVE 1 TO IT_OPCAO
  ELSE INCREMENT IT_OPCAO
  GOTO INICIO
  END

 IF ST_TECLA IN '1234567' BEGIN
                           MOVE (ST_TECLA - 1) TO WINDOWINDEX
                           IF ULTIMO_PROGRAMA.1& NE "" BEGIN
                                                        INDICATE TECLA! TRUE
                                                        MOVE ST_TECLA TO IT_OPCAO
                                                       END
                          END

 GOSUB ALTA_INTENSIDADE
 INDICATE TECLA! GROUP ANY [KEY.RETURN TECLA! KEY.RIGHT]
 [TECLA!] BEGIN
           MOVE (IT_OPCAO - 1) TO WINDOWINDEX
           IF ULTIMO_TIPO.1& EQ "M" BEGIN
                                     CALC (IT_OPCAO - 1) TO WINDOWINDEX
                                     MOVEALL ULTIMO_PROGRAMA.1& TO ST_NOME_MENU
                                     GOSUB ADIANTA_TELA
                                     GOTO INICIO
                                    END
           INDICATE DINAMICO! FALSE
           GOSUB BUSCA_PGM
          END

//-------teclas rapidas

 [~DINAMICO!] IF ST_TECLA IN "0"  BEGIN
              MOVE "" TO ST_SEQUENCIA
              IF ST_TECLA IN "Zz" MOVE "43715"         TO ST_SEQUENCIA
              INDICATE DINAMICO! TRUE
              MOVEALL 0 TO IT_DINAMICO IT_CONTA_MENUS
              MOVE 1 TO IT_OPCAO
              MOVE "" TO ST_COMP
              GOTO INI_TELAS
             END
LOOP
RETURN

MOSTRA_TITULO:
 SCREENMODE IT_COR_TITULO ON
 CABEC MENUTITU.TITULO JN_TARJA_TITULO
 CABEC MENUTITU.TITULO TIT.1
 PAGE TIT
 SCREENMODE IT_COR_TARJA ON
 RETURN

VOLTA_TELA:
 DECREMENT IT_CONTA_MENUS
 IF IT_TELA_CORR EQ 3 MOVE 2 TO IT_TELA_CORR
 ELSE BEGIN
  IF IT_CONTA_MENUS EQ 0 BEGIN
   MOVE ULTIMAS_OPCOES.1 TO IT_OPCAO
   MOVE ULTIMOS_MENUS.1  TO ST_NOME_MENU
   GOSUB LER_OPCOES
   MOVER_MENU 1 1
   MOVE 1 TO IT_TELA_CORR
   GOSUB MOSTRA_TITULO
   PAGE OPCOES_4
   RETURN INICIO
   END
  CALC (IT_CONTA_MENUS - 1) TO WINDOWINDEX
  MOVE ULTIMOS_MENUS.1&     TO ST_NOME_MENU
  MOVE ULTIMAS_OPCOES.1&    TO IT_OPCAO
  GOSUB LER_OPCOES
  MOVER_MENU 1 0
  MOVE 1 TO IT_TELA_CORR
  GOSUB ALTA_INTENSIDADE
  MOVE 2 TO IT_TELA_CORR
  END
 MOVE IT_CONTA_MENUS     TO WINDOWINDEX
 MOVE ULTIMOS_MENUS.1&   TO ST_NOME_MENU
 MOVE ULTIMAS_OPCOES.1&  TO IT_OPCAO
 INDICATE FOLHA_PAGTO! AS ULTIMOS_MENUS.1& EQ "FOLHA_PAGTO"
 [FOLHA_PAGTO!] BEGIN
  SCREENMODE IT_COR_MOLDURA ON
  GOTOXY 5 29
  SHOWLN "                      "
  MOVE "" TO ST_COMP
  END

 GOSUB LER_OPCOES
 GOSUB MOSTRA_TITULO
 IF IT_TELA_CORR EQ 1 MOVER_MENU 1 1
 IF IT_TELA_CORR EQ 2 MOVER_MENU 2 1
 IF IT_TELA_CORR EQ 3 MOVER_MENU 3 1
 RETURN INICIO

ADIANTA_TELA:
 CALC (IT_OPCAO - 1) TO WINDOWINDEX

 MOVEALL ULTIMO_PROGRAMA.1& TO ST_NOME_MENU
 MOVE IT_CONTA_MENUS TO WINDOWINDEX
 MOVE IT_OPCAO TO ULTIMAS_OPCOES.1&
 INCREMENT WINDOWINDEX
 MOVE ST_NOME_MENU TO ULTIMOS_MENUS.1&
 GOSUB LER_OPCOES
 GOSUB MOSTRA_TITULO
 IF IT_TELA_CORR EQ 1 BEGIN
  MOVER_MENU 2 1
  MOVE 1 TO IT_OPCAO
  MOVE 2 TO IT_TELA_CORR
  INCREMENT IT_CONTA_MENUS
  RETURN INICIO
  END

 INDICATE EXISTE_MENU! FALSE
 FOR FIELDINDEX FROM 0 TO 6
  IF MENUCOMP.P_M& EQ "M" INDICATE EXISTE_MENU! TRUE
 LOOP

 [ EXISTE_MENU!] BEGIN
  FOR WINDOWINDEX FROM 0 TO 6
   MOVE OPCOES_2.1& TO OPCOES_1.1&
  LOOP
  MOVE 1 TO IT_TELA_CORR
  GOSUB ALTA_INTENSIDADE
  MOVE 2 TO IT_TELA_CORR
  END

 [~EXISTE_MENU!] MOVE 3 TO IT_TELA_CORR

 MOVE 1 TO IT_OPCAO
 INCREMENT IT_CONTA_MENUS
 IF IT_TELA_CORR EQ 1 MOVER_MENU 1 1
 IF IT_TELA_CORR EQ 2 MOVER_MENU 2 1
 IF IT_TELA_CORR EQ 3 MOVER_MENU 3 1
RETURN INICIO

LER_OPCOES:
 CLEAR MENUCOMP
 CLEAR MENUTITU
 MOVEALL ST_NOME_MENU TO MENUCOMP.NOME MENUTITU.NOME
 FIND EQ MENUCOMP BY INDEX.1
 FIND EQ MENUTITU BY INDEX.1
RETURN

BUSCA_PGM:
 //--
 //--dando uma limpada no userpid
 //--
 CLEAR USERPID
 CONSTRAINT_SET 1
 CONSTRAIN USERPID.COD_ENTIDADE    EQ ST@CODIGO@ENTIDADE
 CONSTRAIN USERPID.EXERCICIO       EQ ST@EXERCICIO@FINANCEIRO
 CONSTRAIN USERPID.CODIGO_USUARIO  EQ ST@USUARIO
 CONSTRAIN USERPID.CODIGO_PROCESSO NE 0
 CONSTRAINED_FIND FIRST USERPID BY INDEX.1
 WHILE [FOUND]
  INDICATE ERR FALSE
  ON ERROR GOSUB CHECA_GRAVACAO
  REREAD USERPID
  DELETE USERPID
  UNLOCK
  ON ERROR OFF
  CONSTRAINED_FIND NEXT
  END
 CONSTRAINT_SET 1 CLEAR

 CLEAR USERPID
 MOVE ST@CODIGO@ENTIDADE      TO USERPID.COD_ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO USERPID.EXERCICIO
 MOVE ST@USUARIO              TO USERPID.CODIGO_USUARIO
 MOVE (PIDATUAL(''))          TO USERPID.CODIGO_PROCESSO
 MOVE (TERMINALATUAL(''))     TO USERPID.TERMINAL
 FIND EQ USERPID BY INDEX.1
 INDICATE ERR FALSE
 ON ERROR GOSUB CHECA_GRAVACAO
 [ FOUND] REREAD USERPID
 MOVE (TRIM(ULTIMO_PROGRAMA.1&)) TO USERPID.PROGRAMA
 SAVERECORD USERPID
 UNLOCK
 ON ERROR OFF
 CLEAR USERPID

//--- setando pgm para usuario
 BUSCA_USUARIO
 TRIM ULTIMO_PROGRAMA.1& TO MENUUSER.PGM_EM_USO
 SAVERECORD MENUUSER
 UNLOCK
 ON ERROR OFF

//--  parte da verificacao WEBSAFE validar instalacao
 MOVE 'sistema_' TO ST_NOME_WEBSAFE
 APPEND ST_NOME_WEBSAFE ST_NOME_RESOLVIDO

//--- gravando horario de entrada
 TRIM ULTIMO_PROGRAMA.1& TO ST_DESTINO

 GRAVA_ENTRADA ' '
 MOVE IT_OPCAO TO IT_AUX

 APPEND ST_DESTINO ' ' ST_NOME_WEBSAFE ' ' MENUUSER.CODIGO ' ' IT_AUX ' ' ULTIMO_PROGRAMA.1& ' ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO // CHAVE P/NAO RODAR POR FORA
 
 //--rotina WEBSAFE para apresentar o nome do menu na imagem JN_TARJA_TITULO_WS da conam.lib
 MOVE MENUTITU.RECNUM TO IT_RECNUM_MENUTITU
 
 MOVE WINDOWINDEX TO IT_GUARDA_WINDOWINDEX
 FOR WINDOWINDEX FROM 0 TO 14
  IF ULTIMOS_MENUS.1& NE '' BEGIN
                             CLEAR MENUTITU
                             MOVE ULTIMOS_MENUS.1& TO MENUTITU.NOME
                             FIND EQ MENUTITU BY INDEX.1
                            END
 LOOP
 MOVE IT_GUARDA_WINDOWINDEX TO WINDOWINDEX
 
 CLEAR MENUTITU
 MOVE IT_RECNUM_MENUTITU TO MENUTITU.RECNUM
 FIND EQ MENUTITU BY RECNUM
 //--
 
 //--caso nao encontre o flx page tela nao-tem
 INDICATE ERR FALSE
 ON ERROR GOSUB LB_NAO_TEM
 CHAIN WAIT ST_DESTINO EXPORT_ONLY
 
 //----- gravando horario de saida
 GRAVA_SAIDA
 
 //----- removendo pgm de usuario
 MOVE (TERMINALATUAL('')) TO ST_TERMINAL_ATUAL
 
 MOVE '' TO ST_GUARDA_PROGRAMA
 CLEAR USERPID
 MOVE ST@CODIGO@ENTIDADE      TO USERPID.COD_ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO USERPID.EXERCICIO
 MOVE ST@USUARIO              TO USERPID.CODIGO_USUARIO
 REPEAT
  FIND GT USERPID BY INDEX.1
  [ FOUND] INDICATE FOUND AS USERPID.COD_ENTIDADE   EQ ST@CODIGO@ENTIDADE
  [ FOUND] INDICATE FOUND AS USERPID.EXERCICIO      EQ ST@EXERCICIO@FINANCEIRO
  [ FOUND] INDICATE FOUND AS USERPID.CODIGO_USUARIO EQ ST@USUARIO
  [ FOUND] BEGIN
            IF USERPID.TERMINAL NE ST_TERMINAL_ATUAL MOVE USERPID.PROGRAMA TO ST_GUARDA_PROGRAMA
           END
 UNTIL [~FOUND]

 //------ seta algum outro programa do usuario em outro terminal ativo
 BUSCA_USUARIO
 MOVE ST_GUARDA_PROGRAMA TO MENUUSER.PGM_EM_USO
 SAVERECORD MENUUSER
 UNLOCK
 ON ERROR OFF
 
 CLEAR USERPID
 MOVE ST@CODIGO@ENTIDADE      TO USERPID.COD_ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO USERPID.EXERCICIO
 MOVE ST@USUARIO              TO USERPID.CODIGO_USUARIO
 REPEAT
  FIND GT USERPID BY INDEX.1
  [ FOUND] INDICATE FOUND AS USERPID.COD_ENTIDADE   EQ ST@CODIGO@ENTIDADE
  [ FOUND] INDICATE FOUND AS USERPID.EXERCICIO      EQ ST@EXERCICIO@FINANCEIRO
  [ FOUND] INDICATE FOUND AS USERPID.CODIGO_USUARIO EQ ST@USUARIO
  [ FOUND] BEGIN
            IF USERPID.TERMINAL EQ ST_TERMINAL_ATUAL BEGIN
                                                      INDICATE ERR FALSE
                                                      ON ERROR GOSUB CHECA_GRAVACAO
                                                      REREAD USERPID
                                                      MOVE '' TO USERPID.PROGRAMA
                                                      SAVERECORD USERPID
                                                      UNLOCK
                                                      ON ERROR OFF
                                                     END
           END
 UNTIL [~FOUND]
 CLEAR USERPID
 
 GOSUB MOSTRA_TELA
RETURN INICIO

MOSTRA_TELA:
 GOSUB DT
 
 PAGE TELA
 PAGE OPCOES_1
 PAGE OPCOES_2
 IF IT_TELA_CORR EQ 3 PAGE OPCOES_3
 IF ST_COMP NE "" BEGIN
  SCREENMODE IT_COR_TITULO ON
  GOTOXY 5 30
  SHOWLN ST_DESCR
  GOTOXY 5 44
  SHOWLN ST_COMP
  END
RETURN

TARJA_TELA:
 DECREMENT IT_TELA_CORR
 CALC (WINDOWINDEX - 1) TO WINDOWINDEX
 MOVE ULTIMAS_OPCOES.1& TO IT_OPCAO
 GOSUB ALTA_INTENSIDADE
RETURN

BAIXA_INTENSIDADE:
 SCREENMODE IT_COR_TARJA ON
 DISPLAYX IT_TELA_CORR
RETURN

ALTA_INTENSIDADE:
 SCREENMODE IT_COR_TITULO ON
 DISPLAYX IT_TELA_CORR
RETURN

PEGA_COR:
 CLEAR MENUUSER
 MOVE ST@USUARIO TO MENUUSER.CODIGO
 FIND EQ MENUUSER BY INDEX.1

 MOVE MENUUSER.COR_MOLDURA  TO IT_COR_MOLDURA  // mover as cores do arquivo
 MOVE MENUUSER.COR_TARJA    TO IT_COR_TARJA    // para as variaveis.
 MOVE MENUUSER.COR_TITULO   TO IT_COR_TITULO
RETURN

KEYPROC KEY.HELP
 AJUDA 'SISTEMA'
 GOSUB MOSTRA_TELA
 GOTOXY 25 00
 ENTAGAIN
RETURN INICIO

KEYPROC KEY.FIELD
 MOVE 'CORES' TO ST_DESTINO
 MOVE MENUTITU.NOME TO ST_NOME_MENU
 APPEND ST_DESTINO ' ' ST_NOME_RESOLVIDO ' ' MENUUSER.CODIGO ' 1 ' ' PROGRAMA ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
 GRAVA_ENTRADA 'CORES'
 CHAIN WAIT ST_DESTINO EXPORT_ONLY
 GRAVA_SAIDA
 KEYPROC ON
 GOSUB MUDA_CORES
RETURN INICIO

KEYPROC KEY.SFIND
 SCREENMODE IT_COR_TITULO ON
 CABEC 'DESEJA REALMENTE ALTERAR SUA SENHA ? <S/N>' CONFIRMA.1
 SCREENMODE IT_COR_TARJA ON
 PAGE CONFIRMA
 INKEYCHECK ST_TECLA IN 'SsNn'
 IF ST_TECLA IN 'sS' BEGIN
                       MOVE 'ASENHOLD' TO ST_DESTINO
                       MOVE MENUTITU.NOME TO ST_NOME_MENU
                       APPEND ST_DESTINO ' ' ST_NOME_RESOLVIDO ' ' MENUUSER.CODIGO ' 1 ' ' PROGRAMA ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
                       GRAVA_ENTRADA 'ASENHOLD'
                       CHAIN WAIT ST_DESTINO EXPORT_ONLY
                       GRAVA_SAIDA
                      END
 ENTAGAIN
 GOSUB MOSTRA_TELA
 GOTOXY 25 00
RETURN INICIO

KEYPROC KEY.USER
 MOVE WINDOWINDEX           TO IT_GUARDA_WINDOWINDEX
 MOVE IT_CONTA_MENUS        TO WINDOWINDEX
 MOVE ULTIMOS_MENUS.1&      TO ST_MENU
 MOVE IT_GUARDA_WINDOWINDEX TO WINDOWINDEX

 MOVE 'GERRELAT' TO ST_DESTINO
 APPEND ST_DESTINO ' ' ST_NOME_RESOLVIDO ' ' MENUUSER.CODIGO ' 1 ' ' PROGRAMA ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
 GRAVA_ENTRADA 'GERENCIADOR'
 CHAIN WAIT ST_DESTINO EXPORT_ONLY
 GRAVA_SAIDA
 GOSUB MOSTRA_TELA
RETURN INICIO

KEYPROC KEY.USER2
 [ LINUX!] BEGIN
            MOVE 'IMPESCOL' TO ST_DESTINO
            APPEND ST_DESTINO ' ' ST_NOME_RESOLVIDO ' ' MENUUSER.CODIGO ' 1 ' ' PROGRAMA ' ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO
            CHAIN WAIT ST_DESTINO EXPORT_ONLY
            GOSUB MOSTRA_TELA
           END
RETURN INICIO

KEYPROC KEY.PRINT
 SCREENMODE IT_COR_TITULO ON
 CABEC 'GOSTARIA REALMENTE DE SAIR <S/N>' CONFIRMA.1
 SCREENMODE IT_COR_TARJA ON
 PAGE CONFIRMA
 INKEYCHECK ST_TECLA IN 'SsNn'
 IF ST_TECLA IN 'sS' BEGIN
                      //limpar userpid do usuario/terminal
                      GOSUB LB_LIMPA_USERPID
                      SCREENMODE IT_COR_TITULO ON
                      ABORT
                     END
 GOSUB MOSTRA_TELA
RETURN INICIO

KEYPROC KEY.DELETE
 GOSUB MOSTRA_TELA
 GOTOXY 25 0
RETURN INICIO

DT:
 SCREENMODE IT_COR_MOLDURA ON
 SYSDATA JN_TARJA_DATA
 SCREENMODE IT_COR_TARJA   ON
RETURN

DIGITA_DATA:
 INDICATE ENTROU! FALSE
 MOVE 'IMUDHORA' TO ST_DESTINO
 APPEND ST_DESTINO ' 1 ' MENUUSER.CODIGO
 CHAIN WAIT ST_DESTINO EXPORT_ONLY
 IF MENUUSER.DATA_TRAB EQ '' BEGIN
                              ADVERTE 'IMPOSS║VEL INICIALIZAR SISTEMA SEM UMA DATA DE TRABALHO V═LIDA!'
                              ABORT
                             END
 SCREENMODE IT_COR_MOLDURA ON
 MOVE MENUUSER.DATA_TRAB TO JN_TARJA_DATA
 SCREENMODE IT_COR_TARJA ON
 PAGE TELA
RETURN

LB_NAO_TEM:
 MOVE 'NAOTEM2 1 ' TO ST_DESTINO
 APPEND ST_DESTINO ' ' ST@USUARIO ' X  PROGRAMA '  ST@CODIGO@ENTIDADE ' ' ST@EXERCICIO@FINANCEIRO ' ' ULTIMO_PROGRAMA.1&
 CHAIN WAIT ST_DESTINO
 GOSUB MOSTRA_TELA
 RETURN INICIO
RETURN

LB_LIMPA_USERPID:
 CLEAR USERPID
 MOVE ST@CODIGO@ENTIDADE      TO USERPID.COD_ENTIDADE
 MOVE ST@EXERCICIO@FINANCEIRO TO USERPID.EXERCICIO
 MOVE ST@USUARIO              TO USERPID.CODIGO_USUARIO
 REPEAT
  FIND GT USERPID BY INDEX.1
  [ FOUND] INDICATE FOUND AS USERPID.COD_ENTIDADE   EQ ST@CODIGO@ENTIDADE
  [ FOUND] INDICATE FOUND AS USERPID.EXERCICIO      EQ ST@EXERCICIO@FINANCEIRO
  [ FOUND] INDICATE FOUND AS USERPID.CODIGO_USUARIO EQ ST@USUARIO
  [ FOUND] BEGIN
            IF USERPID.TERMINAL EQ (TERMINALATUAL('')) BEGIN
                                                        INDICATE ERR FALSE
                                                        ON ERROR GOSUB CHECA_GRAVACAO
                                                        REREAD USERPID
                                                        DELETE USERPID
                                                        UNLOCK
                                                        ON ERROR OFF
                                                       
                                                        CLEAR USERPID
                                                        MOVE ST@CODIGO@ENTIDADE      TO USERPID.COD_ENTIDADE
                                                        MOVE ST@EXERCICIO@FINANCEIRO TO USERPID.EXERCICIO
                                                        MOVE ST@USUARIO              TO USERPID.CODIGO_USUARIO
                                                       END
           END
 UNTIL [~FOUND]
RETURN

#INCLUDE INTEGRID.INC
